
ACTOR RLDemolitionAmmoBattleRifle : RLWeapon
{
  Inventory.Pickupsound "misc/w_pkup"
  Weapon.UpSound "weapons/weaponholster"
  Obituary "%o's head exploded from %k's battle rifle."
  Inventory.Pickupmessage "You picked up a demolition ammo battle rifle."
  Decal BulletChip
  +AMMO_OPTIONAL
  +NOALERT
  +INVENTORY.UNTOSSABLE
  Tag "Demolition Ammo Battle Rifle"
  Weapon.Kickback 40
  Weapon.SelectionOrder 25
  Weapon.SlotNumber 4
  Weapon.AmmoUse1 1
  Weapon.AmmoGive1 0
  Weapon.AmmoType1 "RLDemolitionAmmoBattleRifleClip"
  Weapon.AmmoType2 "Clip"
  States
  {
  Ready:
	TNT1 A 0 A_JumpIfInventory("RLRenegadeAssemblyCheck",1,"RenegadeAssemblyNoFire")
	TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoBattleRifleClip",1,2)
    UACR B 1 A_WeaponReady (WRF_ALLOWRELOAD | WRF_ALLOWZOOM)
    Loop
    UACR A 1 A_WeaponReady (WRF_ALLOWRELOAD | WRF_ALLOWZOOM)
    Loop
  ScavengerDrop:
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"WeaponDropConfirm")
	TNT1 A 0 A_GiveInventory("RLScavengerDrop",1)
	TNT1 A 0 A_Print("Press again to drop current weapon, or press Alt Fire to scrap it for a Mod Pack.")
	Goto AfterModDrop
  WeaponDropConfirm:
	TNT1 A 0 A_JumpIfInventory("RLShellshockPerk",1, 1)
	Goto WeaponDropConfirm+2
	TNT1 A 0 A_GiveInventory("RLRenegadeAssemblyCheck",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLDemolitionAmmoBattleRifleSelected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
	TNT1 A 0 A_FireCustomMissile("DroppedRLDemolitionAmmoBattleRifle",0,0,0)
	TNT1 A 1 A_TakeInventory("RLDemolitionAmmoBattleRifle",1)
	Stop
  AfterModDrop:
	TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoBattleRifleClip",1,2)
    UACR B 8 A_WeaponReady (16)
    Goto Ready
    UACR A 8 A_WeaponReady (16)
    Goto Ready
  RenegadeAssemblyNoFire:
	TNT1 A 0 A_TakeInventory("RLRenegadeAssemblyCheck",1)
	TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoBattleRifleClip",1,2)
    UACR B 10 A_WeaponReady (WRF_NOFIRE | WRF_NOBOB)
    Goto Ready
    UACR A 10 A_WeaponReady (WRF_NOFIRE | WRF_NOBOB)
    Goto Ready
  Deselect:
	TNT1 A 0
	TNT1 A 0 A_TakeInventory("RLArmorModItemActivate",1)
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
	TNT1 A 0 A_TakeInventory("RLUseBulkMod",1)
	TNT1 A 0 A_TakeInventory("RLUsePowerMod",1)
	TNT1 A 0 A_TakeInventory("RLUseAgilityMod",1)
	TNT1 A 0 A_TakeInventory("RLUseTechnicalMod",1)
	TNT1 A 0 A_TakeInventory("RLUseNanoMod",1)
	TNT1 A 0 A_TakeInventory("RLUseFirestormMod",1)
	TNT1 A 0 A_TakeInventory("RLUseSniperMod",1)
	TNT1 A 0 A_TakeInventory("RLDemolitionAmmoBattleRifleSelected", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
  DeselectLoop:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "DeselectRenegade")
	TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoBattleRifleClip",1,2)
    UACR B 1 A_Lower
    Loop
    UACR A 1 A_Lower
    Loop
  DeselectRenegade:
    TNT1 A 0 A_Lower
    loop
  SelectRenegade: 
    TNT1 A 0 A_Raise
    loop
  SelectSetWeapon:
	TNT1 A 0 A_GiveInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_GiveInventory("RLDemolitionAmmoBattleRifleSelected", 1)
    Goto SelectLoop
  SelectLoop:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "SelectRenegade")
	TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoBattleRifleClip",1,2)
    UACR B 1 A_Raise
    Loop
    UACR A 1 A_Raise
    Loop
  Fire:
	TNT1 A 0 A_JumpIfNoAmmo("Reload")
	TNT1 A 0 A_AlertMonsters
	TNT1 A 0 A_GiveInventory("RLDemolitionAmmoBattleRifleAmmochain",1)
	TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoBattleRifleAmmochain",4,2)
	TNT1 A 0 A_JumpIfInventory("RLAmmochainPerk",1,2)
	TNT1 A 0 A_FireBullets (2, 0.5, -1, 5, "RLDemoBattleRiflePuff")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (2, 0.5, -1, 5, "RLDemoBattleRiflePuff", "None")
    Goto FireFinish
  FireFinish:
    TNT1 A 0 A_PlaySound("weapons/demobattlerifle", CHAN_WEAPON)
    UACR Y 1 Bright A_GunFlash
    UACR Z 1 Bright
	TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoBattleRifleClip",1,3)
    UACR VB 1
    Goto FireFinishContinue
    UACR WA 1
    Goto FireFinishContinue
  FireFinishContinue:
	TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoBattleRifleAmmochain",4,2)
	TNT1 A 0 A_Jump(256,2)
	TNT1 A 0 A_TakeInventory("RLDemolitionAmmoBattleRifleAmmochain",4)
    TNT1 A 0 A_ReFire
    Goto Ready
  Reload:
	TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoBattleRifleClip",1,2)
	UACR B 1 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
	Goto Reload+2
	UACR A 1 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
	TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoBattleRifleClip", 20, "Ready")
	TNT1 A 0 A_JumpIfInventory("Clip", 1, 1)
	Goto Ready
	TNT1 A 0 // Play the reload animation.
	TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoBattleRifleClip",1,11)
    UACR N 2
    UACR O 2
    UACR P 2
    TNT1 A 0 A_PlaySound("weapons/demobattlerifleunload", 5)
    UACR Q 2
    UACR R 8
    UACR S 2
    UACR T 2
    UACR U 2
//    TNT1 A 0 A_PlaySound("weapons/demobattleriflecock", 7)
	Goto ReloadWorking
    UACR C 2
    UACR D 2
    UACR E 2
    TNT1 A 0 A_PlaySound("weapons/demobattlerifleunload", 5)
    UACR F 2
    UACR G 8
    UACR H 2
    UACR I 2
    UACR J 2
	Goto ReloadWorking
  ReloadWorking:
	TNT1 A 0 A_TakeInventory("Clip", 1)
	TNT1 A 0 A_GiveInventory("RLDemolitionAmmoBattleRifleClip", 1)
	TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoBattleRifleClip",20,2)
	TNT1 A 0 A_JumpIfInventory("Clip",1,2)
	TNT1 A 0
	Goto ReloadFinish
	TNT1 A 0
	Goto ReloadWorking
  ReloadFinish:
    TNT1 A 0 A_PlaySound("weapons/demobattlerifleload", 6)
    UACR K 3
    UACR L 5
    UACR M 3
    UACR A 6
	Goto Ready
  Flash:
    TNT1 A 1 A_Light1
    TNT1 A 1 A_Light2
    Goto LightDone
  Spawn:
    UACR X -1
    Stop
  Altfire:
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate",1, "ArmorModConversion")
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"ScavengerTime")
	Goto Ready
  ScavengerTime:
	TNT1 A 0 A_Print("Mod pack extracted from discarded weapon.")
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLDemolitionAmmoBattleRifleSelected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
    TNT1 A 0 A_StopSound(6)
	TNT1 A 0 A_Jump(256,1,2,3,4)
	TNT1 A 0 A_FireCustomMissile("DroppedRLFirestormMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLTechnicalMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLTechnicalMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLPowerMod",0,0,0)
	Goto ScavengerTimeEnd
  ScavengerTimeEnd:
	TNT1 A 1 A_TakeInventory("RLDemolitionAmmoBattleRifle",1)
	Stop
  }
}

//-------------------------------------------------------------------
//		PROJECTILES

ACTOR RLDemoBattleRiflePuff
{ 
   +NOGRAVITY
   +NOBLOCKMAP
   +PUFFONACTORS
   +PUFFGETSOWNER
   +RANDOMIZE
   +THRUGHOST
   RenderStyle Add
   Alpha 0.9
   Mass 5
   Scale 1.0
   Decal "RevenantScorch"
   Damagetype "Fire"
   States
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_PlaySound("weapons/bulletexp", 7, 0.5, 0, 0.5)
      TNT1 A 0 A_Explode (100,40, 0, 1)
      MICX ABCDEFG 2 Bright
      Stop
   }
}

//-------------------------------------------------------------------
//		MECHANICS

Actor RLDemolitionAmmoBattleRifleSelected : Inventory
{
inventory.maxamount 1
}

Actor RLDemolitionAmmoBattleRifleAmmochain : Inventory
{
inventory.maxamount 4
}

ACTOR RLDemolitionAmmoBattleRifleClip : Ammo
{
   +INVENTORY.IGNORESKILL
   Inventory.MaxAmount 20
   Inventory.Icon ""
}

ACTOR RLDemolitionAmmoBattleRiflePickup : CustomInventory
{
	Radius 16
	Height 8
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
	Spawn:
		UACR X -1
		Stop
	Pickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoBattleRifle",1,"NotPickup") // If the player already has this weapon, they are not given the weapon
		TNT1 A 0 A_JumpIfInventory("RLWeaponLimit",0,"NotPickup2") // The absolute max limit check for weapons
		TNT1 A 0 A_PlaySound("weapons/demobattleriflepickup")
		TNT1 A 0 A_GiveInventory("RLDemolitionAmmoBattleRifle",1)
		TNT1 A 0 A_GiveInventory("RLWeaponLimit",1)
		TNT1 A 0 A_Print("Picked up a \cvDemolition Ammo Battle Rifle.")
		Stop
	NotPickup:
		TNT1 A 0 A_JumpIfInventory("Clip",0,1)
		Goto AmmoPickup
	NotPickup2:
		TNT1 A 0 A_JumpIfInventory("DRLA_WeaponAntiLogFloodCheck",1,"NotPickup_NoPrint")
		TNT1 A 0 A_GiveInventory("DRLA_WeaponAntiLogFloodCheck")
		TNT1 A 0 A_Print("\cv=M= Demolition Ammo Battle Rifle =M=", 2, "CONFONT")
	 NotPickup_NoPrint:	
		TNT1 A 0
		Fail
	AmmoPickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("Clip",0,"AmmoFull")
		TNT1 A 0 A_PlaySound("misc/w_pkup")
		TNT1 A 1 A_GiveInventory("Clip",20)
		Stop
	AmmoFull:
		UACR X 1
		Fail
	}
}

ACTOR DroppedRLDemolitionAmmoBattleRifle
{
	PROJECTILE
	-NOGRAVITY
	Damage 0
	Radius 16
	Height 8
	Speed 9
	DeathSound "weapons/gundrop"
	States
	{
	Spawn:
		UACR X 1
		Loop
	Death:
		TNT1 A 0
		UACR X 1 A_SpawnItem("RLDemolitionAmmoBattleRiflePickup",0,0,0,0)
		Stop
	}
}
