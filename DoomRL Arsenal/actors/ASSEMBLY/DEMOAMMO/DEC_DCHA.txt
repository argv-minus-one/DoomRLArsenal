
ACTOR RLDemolitionAmmoChaingun : RLWeapon
{
  Inventory.Pickupsound "misc/w_pkup"
  Weapon.UpSound "weapons/weaponholster"
  Obituary "%o was perforated by %k's chaingun."
  Inventory.Pickupmessage "You picked up a Demolition Ammo Chaingun."
  Decal BulletChip
  +AMMO_OPTIONAL
  +NOALERT
  +INVENTORY.UNTOSSABLE
  Tag "Demolition Ammo Chaingun"
  Weapon.Kickback 40
  Weapon.SelectionOrder 25
  Weapon.SlotNumber 4
  Weapon.AmmoUse1 1
  Weapon.AmmoGive1 0
  Weapon.AmmoType1 "RLDemolitionAmmoChaingunClip"
  Weapon.AmmoType2 "Clip"
  States
  {
  Ready:
	TNT1 A 0 A_JumpIfInventory("RLRenegadeAssemblyCheck",1,"RenegadeAssemblyNoFire")
	TNT1 A 0 A_TakeInventory("RLDemolitionAmmoChaingunState",1)
    BCHG A 1 A_WeaponReady (48)
    Goto Ready+2
  ScavengerDrop:
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"WeaponDropConfirm")
	TNT1 A 0 A_GiveInventory("RLScavengerDrop",1)
	TNT1 A 0 A_Print("Press again to drop current weapon, or press Alt Fire to scrap it for a Mod Pack.")
	Goto AfterModDrop
  WeaponDropConfirm:
	TNT1 A 0 A_JumpIfInventory("RLShellshockPerk",1, 1)
	Goto WeaponDropConfirm+2
	TNT1 A 0 A_GiveInventory("RLRenegadeAssemblyCheck",1)
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLDemolitionAmmoChaingunSelected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
	TNT1 A 0 A_FireCustomMissile("DroppedRLDemolitionAmmoChaingun",0,0,0)
	TNT1 A 1 A_TakeInventory("RLDemolitionAmmoChaingun",1)
	Stop
  AfterModDrop:
    BCHG A 8 A_WeaponReady (16)
    Goto Ready
  RenegadeAssemblyNoFire:
	TNT1 A 0 A_TakeInventory("RLRenegadeAssemblyCheck",1)
    BCHG A 9 A_WeaponReady (WRF_NOFIRE | WRF_NOBOB)
    Goto Ready
  Deselect:
	TNT1 A 0
	TNT1 A 0 A_TakeInventory("RLArmorModItemActivate",1)
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
	TNT1 A 0 A_TakeInventory("RLUseBulkMod",1)
	TNT1 A 0 A_TakeInventory("RLUsePowerMod",1)
	TNT1 A 0 A_TakeInventory("RLUseAgilityMod",1)
	TNT1 A 0 A_TakeInventory("RLUseTechnicalMod",1)
	TNT1 A 0 A_TakeInventory("RLUseNanoMod",1)
	TNT1 A 0 A_TakeInventory("RLUseFirestormMod",1)
	TNT1 A 0 A_TakeInventory("RLUseSniperMod",1)
	TNT1 A 0 A_TakeInventory("RLDemolitionAmmoChaingunSelected", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
  DeselectLoop:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "DeselectRenegade")
    BCHG A 1 A_Lower
    Loop
  DeselectRenegade:
    TNT1 A 0 A_Lower
    loop
  SelectRenegade: 
    TNT1 A 0 A_Raise
    loop
  SelectSetWeapon:
	TNT1 A 0 A_GiveInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_GiveInventory("RLDemolitionAmmoChaingunSelected", 1)
    Goto SelectLoop
  SelectLoop:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "SelectRenegade")
    BCHG A 1 A_Raise
    Loop
  Fire:
	TNT1 A 0 A_JumpIfNoAmmo("Reload")
	TNT1 A 0 A_AlertMonsters
	TNT1 A 0 A_GiveInventory("RLDemolitionAmmoChaingunAmmochain",1)
	TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoChaingunAmmochain",4,2)
	TNT1 A 0 A_JumpIfInventory("RLAmmochainPerk",1,2)
	TNT1 A 0 A_FireBullets (6, 2, -1, 5, "RLDemoChaingunPuff")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (6, 2, -1, 5, "RLDemoChaingunPuff", "None")
    Goto FireFinish
  FireFinish:
    TNT1 A 0 A_PlaySound("weapons/demochaingun", CHAN_WEAPON)
    BCHG A 1 A_GunFlash
    BCHG BCD 1
	TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoChaingunAmmochain",4,2)
	TNT1 A 0 A_Jump(256,2)
	TNT1 A 0 A_TakeInventory("RLDemolitionAmmoChaingunAmmochain",4)
	TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoChaingunState",1,2)
	TNT1 A 0 A_GiveInventory("RLDemolitionAmmoChaingunState",1)
    Goto Fire
	TNT1 A 0 A_TakeInventory("RLDemolitionAmmoChaingunState",1)
    TNT1 A 0 A_ReFire
	BCHG AB 2
	BCHG CD 3
	TNT1 A 0 A_JumpIfNoAmmo(2)
	BCHG A 4 A_WeaponReady(WRF_NOBOB | WRF_NOSWITCH)
	BCHG B 4
	BCHG CD 5
    Goto Ready
  Reload:
	BCHG A 1 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
	TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoChaingunClip", 40, "Ready")
	TNT1 A 0 A_JumpIfInventory("Clip", 1, 1)
	Goto Ready
	TNT1 A 0 // Play the reload animation.
    BCHG I 5
    BCHG J 5
    BCHG K 5
    BCHG L 5 A_PlaySound("weapons/demochaingununload", 5)
    BCHG L 5
  ReloadWorking:
	TNT1 A 0 A_TakeInventory("Clip", 1)
	TNT1 A 0 A_GiveInventory("RLDemolitionAmmoChaingunClip", 1)
	TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoChaingunClip",40,2)
	TNT1 A 0 A_JumpIfInventory("Clip",1,2)
	TNT1 A 0
	Goto ReloadFinish
	TNT1 A 0
	Goto ReloadWorking
  ReloadFinish:
    TNT1 A 0 A_PlaySound("weapons/demochaingunload", 7)
    BCHG M 5
    BCHG L 5
    BCHG K 5
    BCHG J 5
    BCHG I 5
	Goto ReloadSpin
  ReloadSpin:
    TNT1 A 0 A_PlaySound("weapons/demochaingunwinddown", 6)
	BCHG AB 3
	BCHG CD 4
	TNT1 A 0 A_JumpIfNoAmmo(1)
	BCHG AB 5 A_WeaponReady(WRF_NOSWITCH)
	BCHG CD 6 A_WeaponReady(WRF_NOSWITCH)
	Goto Ready
  Flash:
	TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoChaingunState",1,3)
    BCHG E 1 Bright A_Light2
    BCHG F 1 Bright A_Light1
    Goto LightDone
    BCHG G 1 Bright A_Light2
    BCHG H 1 Bright A_Light1
    Goto LightDone
  Spawn:
    BCHG X -1
    Stop
  Altfire:
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate",1, "ArmorModConversion")
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"ScavengerTime")
	Goto Ready
  ScavengerTime:
	TNT1 A 0 A_Print("Mod pack extracted from discarded weapon.")
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLDemolitionAmmoChaingunSelected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
	TNT1 A 0 A_Jump(256,1,2,3,4)
	TNT1 A 0 A_FireCustomMissile("DroppedRLPowerMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLTechnicalMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLTechnicalMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLFirestormMod",0,0,0)
	Goto ScavengerTimeEnd
  ScavengerTimeEnd:
	TNT1 A 1 A_TakeInventory("RLDemolitionAmmoChaingun",1)
	Stop
  }
}

//-------------------------------------------------------------------
//		PROJECTILES

ACTOR RLDemoChaingunPuff
{ 
   +NOGRAVITY
   +NOBLOCKMAP
   +PUFFONACTORS
   +PUFFGETSOWNER
   +RANDOMIZE
   +THRUGHOST
   RenderStyle Add
   Alpha 0.9
   Mass 5
   Scale 0.8
   Decal "RevenantScorch"
   Damagetype "Fire"
   States
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_PlaySound("weapons/bulletexp", 7, 0.5, 0, 0.5)
      TNT1 A 0 A_Explode (48,20, 0, 1)
      MICX ABCDEFG 2 Bright
      Stop
   }
}

//-------------------------------------------------------------------
//		MECHANICS

Actor RLDemolitionAmmoChaingunSelected : Inventory
{
inventory.maxamount 1
}

Actor RLDemolitionAmmoChaingunState : Inventory
{
inventory.maxamount 1
}

Actor RLDemolitionAmmoChaingunAmmochain : Inventory
{
inventory.maxamount 4
}

ACTOR RLDemolitionAmmoChaingunClip : Ammo
{
   +INVENTORY.IGNORESKILL
   Inventory.MaxAmount 40
   Inventory.Icon ""
}

ACTOR RLDemolitionAmmoChaingunPickup : CustomInventory
{
	Radius 16
	Height 8
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
	Spawn:
		BCHG X -1
		Stop
	Pickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("RLDemolitionAmmoChaingun",1,"NotPickup") // If the player already has this weapon, they are not given the weapon
		TNT1 A 0 A_JumpIfInventory("RLWeaponLimit",0,"NotPickup2") // The absolute max limit check for weapons
		TNT1 A 0 A_PlaySound("weapons/demochaingunpickup")
		TNT1 A 0 A_GiveInventory("RLDemolitionAmmoChaingun",1)
		TNT1 A 0 A_GiveInventory("RLWeaponLimit",1)
		TNT1 A 0 A_Print("Picked up a \cvDemolition Ammo Chaingun.")
		Stop
	NotPickup:
		TNT1 A 0 A_JumpIfInventory("Clip",0,1)
		Goto AmmoPickup
	NotPickup2:
		TNT1 A 0 A_JumpIfInventory("DRLA_WeaponAntiLogFloodCheck",1,"NotPickup_NoPrint")
		TNT1 A 0 A_GiveInventory("DRLA_WeaponAntiLogFloodCheck")
		TNT1 A 0 A_Print("\cv=M= Demolition Ammo Chaingun =M=", 2, "CONFONT")
	 NotPickup_NoPrint:	
		TNT1 A 0
		Fail
	AmmoPickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("Clip",0,"AmmoFull")
		TNT1 A 0 A_PlaySound("misc/w_pkup")
		TNT1 A 1 A_GiveInventory("Clip",40)
		Stop
	AmmoFull:
		BCHG X 1
		Fail
	}
}

ACTOR DroppedRLDemolitionAmmoChaingun
{
	PROJECTILE
	-NOGRAVITY
	Damage 0
	Radius 16
	Height 8
	Speed 9
	DeathSound "weapons/gundrop"
	States
	{
	Spawn:
		BCHG X 1
		Loop
	Death:
		TNT1 A 0
		BCHG X 1 A_SpawnItem("RLDemolitionAmmoChaingunPickup",0,0,0,0)
		Stop
	}
}
