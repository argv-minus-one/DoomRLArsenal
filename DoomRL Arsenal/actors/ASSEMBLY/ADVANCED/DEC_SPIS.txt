
ACTOR RLStormPistol : RLWeapon
{
  Inventory.Pickupsound "misc/w_pkup"
  Weapon.UpSound "weapons/weaponholster"
  Obituary "%o was bolted by %k's storm pistol."
  Inventory.Pickupmessage "You picked up a Storm Pistol."
  Decal BulletChip
  +AMMO_OPTIONAL
  +NOALERT
  +INVENTORY.UNTOSSABLE
  Tag "Storm Pistol"
  Weapon.Kickback 40
  Weapon.SelectionOrder 1900
  Weapon.SlotNumber 2
  Weapon.AmmoUse1 1
  Weapon.AmmoGive1 0
  Weapon.AmmoType1 "RLStormPistolClip"
  Weapon.AmmoType2 "Clip"
  States
  {
  Ready:
	TNT1 A 0 A_JumpIfInventory("RLRenegadeAssemblyCheck",1,"RenegadeAssemblyNoFire")
	TNT1 A 0 A_JumpIfInventory("RLStormPistolNanoMod",1,2)
    SPIS A 1 A_WeaponReady (48)
    Loop
  NanoRegenCheck:
	TNT1 A 0 A_JumpIfInventory("RLStormPistolClip",1,2)
    SPIS A 70 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
	Goto NanoRegenCheckBegin
  NanoRegenCheckBegin:
    SPIS A 70 A_WeaponReady (32)
	TNT1 A 0 A_JumpIfInventory("RLStormPistolBulkMod",1,2)
	TNT1 A 0 A_JumpIfInventory("RLStormPistolClip", 9, "Ready")
	Goto NanoRegen
	TNT1 A 0 A_JumpIfInventory("RLStormPistolClip", 12, "Ready")
	Goto NanoRegen
  NanoRegen:
	TNT1 A 0 A_GiveInventory("RLStormPistolClip", 1)
	TNT1 A 0 A_JumpIfInventory("RLStormPistolNanoMod",2,2)
    SPIS A 17 A_WeaponReady (32)
    Goto NanoRegenCheckBegin+1
    SPIS A 9 A_WeaponReady (32)
    Goto NanoRegenCheckBegin+1
  ScavengerDrop:
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"WeaponDropConfirm")
	TNT1 A 0 A_GiveInventory("RLScavengerDrop",1)
	TNT1 A 0 A_Print("Press again to drop current weapon, or press Alt Fire to scrap it for a Mod Pack.")
	Goto AfterModDrop
  WeaponDropConfirm:
	TNT1 A 0 A_JumpIfInventory("RLShellshockPerk",1, 1)
	Goto WeaponDropConfirm+2
	TNT1 A 0 A_GiveInventory("RLRenegadeAssemblyCheck",1)
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
    TNT1 A 0 A_TakeInventory("RLStormPistolModLimit", 1)
    TNT1 A 0 A_TakeInventory("RLStormPistolPowerMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormPistolBulkMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormPistolAgilityMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormPistolTechnicalMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormPistolSniperMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormPistolFirestormMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormPistolNanoMod", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLStormPistolSelected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
	TNT1 A 0 A_FireCustomMissile("DroppedRLStormPistol",0,0,0)
	TNT1 A 1 A_TakeInventory("RLStormPistol",1)
	Stop
  AfterModDrop:
    SPIS A 8 A_WeaponReady (16)
    Goto Ready
  RenegadeAssemblyNoFire:
	TNT1 A 0 A_TakeInventory("RLRenegadeAssemblyCheck",1)
    SPIS A 9 A_WeaponReady (WRF_NOFIRE | WRF_NOBOB)
    Goto Ready
  Deselect:
	TNT1 A 0
	TNT1 A 0 A_TakeInventory("RLArmorModItemActivate",1)
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
	TNT1 A 0 A_TakeInventory("RLUseBulkMod",1)
	TNT1 A 0 A_TakeInventory("RLUsePowerMod",1)
	TNT1 A 0 A_TakeInventory("RLUseAgilityMod",1)
	TNT1 A 0 A_TakeInventory("RLUseTechnicalMod",1)
	TNT1 A 0 A_TakeInventory("RLUseNanoMod",1)
	TNT1 A 0 A_TakeInventory("RLUseFirestormMod",1)
	TNT1 A 0 A_TakeInventory("RLUseSniperMod",1)
	TNT1 A 0 A_TakeInventory("RLStormPistolSelected", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
  DeselectLoop:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "DeselectRenegade")
    TNT1 A 0 A_Lower
    SPIS A 1 A_Lower
    Loop
  DeselectRenegade:
    TNT1 A 0 A_Lower
    loop
  SelectRenegade: 
    TNT1 A 0 A_Raise
    loop
  SelectSetWeapon:
	TNT1 A 0 A_GiveInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_GiveInventory("RLStormPistolSelected", 1)
    Goto SelectLoop
  SelectLoop:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "SelectRenegade")
    TNT1 A 0 A_Raise
    SPIS A 1 A_Raise
    Loop
  Fire:
	TNT1 A 0 A_JumpIfNoAmmo("Reload")
    SPIS A 1
	TNT1 A 0 A_AlertMonsters
	TNT1 A 0 A_JumpIfInventory("RLStormPistolSniperMod",1,"FireS")
	TNT1 A 0 A_JumpIfInventory("RLStormPistolPowerMod",1,"FireP")
	TNT1 A 0 A_JumpIfInventory("RLStormPistolAgilityMod",1,"FireA")
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "FireRenegade")
	TNT1 A 0 A_JumpIfInventory("RLSharpshooterPerk",1,2)
	TNT1 A 0 A_FireBullets (7, 4, 2, 6, "RLBulletPuff")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (7, 4, 2, (48), "RLBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
    Goto FireFinish
  FireRenegade:
	TNT1 A 0 A_FireBullets (7, 4, 3, 6, "RLBulletPuff")
    Goto FireFinish
  FireP:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "FireRenegadeP")
	TNT1 A 0 A_JumpIfInventory("RLSharpshooterPerk",1,2)
	TNT1 A 0 A_FireBullets (7, 4, 2, 7, "RLBulletPuff")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (7, 4, 2, (56), "RLBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
    Goto FireFinish
  FireRenegadeP:
	TNT1 A 0 A_FireBullets (7, 4, 3, 7, "RLBulletPuff")
    Goto FireFinish
  FireA:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "FireRenegadeA")
	TNT1 A 0 A_JumpIfInventory("RLSharpshooterPerk",1,2)
	TNT1 A 0 A_FireBullets (6.5, 3.5, 2, 6, "RLBulletPuff")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (6.5, 3.5, 2, (48), "RLBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
    Goto FireFinish
  FireRenegadeA:
	TNT1 A 0 A_FireBullets (6.5, 3.5, 3, 6, "RLBulletPuff")
    Goto FireFinish
  FireS:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "FireRenegadeS")
	TNT1 A 0 A_JumpIfInventory("RLSharpshooterPerk",1,2)
	TNT1 A 0 A_FireBullets (5, 2, 2, 6, "RLBulletPuff")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (5, 2, 2, (48), "RLBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
    Goto FireFinish
  FireRenegadeS:
	TNT1 A 0 A_FireBullets (5, 2, 3, 6, "RLBulletPuff")
    Goto FireFinish
  FireFinish:
    TNT1 A 0 A_PlaySound("weapons/stormpistol", CHAN_WEAPON)
	TNT1 A 0 A_JumpIfInventory("RLStormPistolFirestormMod",1,"FireFinishF1")
    SPIS B 2 Bright A_GunFlash
    SPIS CD 2
    SPIS A 3
    SPIS A 1 A_ReFire
    Goto Ready
  FireFinishF1:
    SPIS B 1 Bright A_GunFlash
    SPIS CD 1
    SPIS A 3
    SPIS A 1 A_ReFire
    Goto Ready
  Reload:
	TNT1 A 0 A_JumpIfInventory("RLStormPistolNanoMod",1,"Ready")
	SPIS A 1 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
	TNT1 A 0 A_JumpIfInventory("RLStormPistolBulkMod",1,2)
	TNT1 A 0 A_JumpIfInventory("RLStormPistolClip", 9, "Ready")
	Goto Reload+5
	TNT1 A 0 A_JumpIfInventory("RLStormPistolClip", 12, "Ready")
	TNT1 A 0 A_JumpIfInventory("Clip", 1, 1)
	Goto Ready
	TNT1 A 0 // Play the reload animation.
	TNT1 A 0 A_JumpIfInventory("RLStormPistolTechnicalMod",1,"Reload1")
    SPIS E 2
    SPIS F 2
    TNT1 A 0 A_PlaySound("weapons/stormpistolunload", 5)
    SPIS G 6
    SPIS F 2
    SPIS H 2
    SPIS I 2
    SPIS J 2
    SPIS K 2
    SPIS L 2
    SPIS M 2
	Goto ReloadWorking
  Reload1:
    SPIS E 1
    SPIS F 1
    TNT1 A 0 A_PlaySound("weapons/stormpistolunload", 5)
    SPIS G 6
    SPIS F 1
    SPIS H 2
    SPIS I 2
    SPIS J 2
    SPIS K 2
    SPIS L 2
    SPIS M 2
  ReloadWorking:
	TNT1 A 0 A_TakeInventory("Clip", 1)
	TNT1 A 0 A_GiveInventory("RLStormPistolClip", 1)
	TNT1 A 0 A_JumpIfInventory("RLStormPistolBulkMod",1,2)
	TNT1 A 0 A_JumpIfInventory("RLStormPistolClip",9,3)
	Goto ReloadWorking+5
	TNT1 A 0 A_JumpIfInventory("RLStormPistolClip",12,2)
	TNT1 A 0 A_JumpIfInventory("Clip",1,2)
	TNT1 A 0
	Goto ReloadFinish
	TNT1 A 0
	Goto ReloadWorking
  ReloadFinish:
	TNT1 A 0 A_JumpIfInventory("RLStormPistolTechnicalMod",1,"ReloadFinish1")
    TNT1 A 0 A_PlaySound("weapons/stormpistolload", 6)
    SPIS N 6
    SPIS M 2
    SPIS O 2
    SPIS A 2
	Goto Ready
  ReloadFinish1:
    TNT1 A 0 A_PlaySound("weapons/stormpistolload", 6)
    SPIS N 6
    SPIS M 1
    SPIS O 1
    SPIS A 1
	Goto Ready
  Flash:
    TNT1 A 2 Bright A_Light2
    TNT1 A 1 Bright A_Light1
    Goto LightDone
  Spawn:
    SPIG A -1
    Stop
  Altfire:
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate",1, "ArmorModConversion")
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"ScavengerTime")
	Goto Ready
  ScavengerTime:
	TNT1 A 0 A_Print("Mod pack extracted from discarded weapon.")
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
    TNT1 A 0 A_TakeInventory("RLStormPistolModLimit", 1)
    TNT1 A 0 A_TakeInventory("RLStormPistolPowerMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormPistolBulkMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormPistolAgilityMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormPistolTechnicalMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormPistolSniperMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormPistolFirestormMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormPistolNanoMod", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLStormPistolSelected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
	TNT1 A 0 A_Jump(256,1,2,3)
	TNT1 A 0 A_FireCustomMissile("DroppedRLBulkMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLBulkMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLTechnicalMod",0,0,0)
	Goto ScavengerTimeEnd
  ScavengerTimeEnd:
	TNT1 A 1 A_TakeInventory("RLStormPistol",1)
	Stop
  }
}

//-------------------------------------------------------------------
//		MECHANICS

Actor RLStormPistolSelected : Inventory
{
inventory.maxamount 1
}

Actor RLStormPistolModLimit : Inventory
{
inventory.maxamount 1
}

Actor RLStormPistolPowerMod : Inventory
{
inventory.maxamount 1
}

Actor RLStormPistolBulkMod : Inventory
{
inventory.maxamount 1
}

Actor RLStormPistolAgilityMod : Inventory
{
inventory.maxamount 1
}

Actor RLStormPistolTechnicalMod : Inventory
{
inventory.maxamount 1
}

Actor RLStormPistolNanoMod : Inventory
{
inventory.maxamount 1
}

Actor RLStormPistolFirestormMod : Inventory
{
inventory.maxamount 1
}

Actor RLStormPistolSniperMod : Inventory
{
inventory.maxamount 1
}

ACTOR RLStormPistolClip : Ammo
{
   +INVENTORY.IGNORESKILL
   Inventory.MaxAmount 14
   Inventory.Icon ""
}

ACTOR RLStormPistolPickup : CustomInventory
{
	Radius 16
	Height 8
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
	Spawn:
		SPIG A -1
		Stop
	Pickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("RLStormPistol",1,"NotPickup") // If the player already has this weapon, they are not given the weapon
		TNT1 A 0 A_JumpIfInventory("RLWeaponLimit",0,"NotPickup2") // The absolute max limit check for weapons
		TNT1 A 0 A_PlaySound("weapons/stormpistolpickup")
		TNT1 A 0 A_GiveInventory("RLStormPistol",1)
		TNT1 A 0 A_GiveInventory("RLWeaponLimit",1)
		TNT1 A 0 A_Print("Picked up a \cvStorm Pistol.")
		Stop
	NotPickup:
		TNT1 A 0 A_JumpIfInventory("Clip",0,1)
		Goto AmmoPickup
	NotPickup2:
		TNT1 A 0 A_JumpIfInventory("DRLA_WeaponAntiLogFloodCheck",1,"NotPickup_NoPrint")
		TNT1 A 0 A_GiveInventory("DRLA_WeaponAntiLogFloodCheck")
		TNT1 A 0 A_Print("\cv= Storm Pistol =", 2, "CONFONT")
	 NotPickup_NoPrint:	
		TNT1 A 0
		Fail
	AmmoPickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("Clip",0,"AmmoFull")
		TNT1 A 0 A_PlaySound("misc/w_pkup")
		TNT1 A 1 A_GiveInventory("Clip",9)
		Stop
	AmmoFull:
		SPIG A 1
		Fail
	}
}

ACTOR DroppedRLStormPistol
{
	PROJECTILE
	-NOGRAVITY
	Damage 0
	Radius 16
	Height 8
	Speed 9
	DeathSound "weapons/gundrop"
	States
	{
	Spawn:
		SPIG A 1
		Loop
	Death:
		TNT1 A 0
		SPIG A 1 A_SpawnItem("RLStormPistolPickup",0,0,0,0)
		Stop
	}
}
