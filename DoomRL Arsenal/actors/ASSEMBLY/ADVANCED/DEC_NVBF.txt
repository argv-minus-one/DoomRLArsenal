
ACTOR RLNuclearVBFG9000 : RLWeapon
{
  Inventory.Pickupsound "misc/w_pkup"
  Weapon.UpSound "weapons/weaponholster"
  Obituary "%o was utterly annihilated by %k's VBFG."
  Inventory.Pickupmessage "You picked up a Nuclear VBFG9000. Oh yes."
  +AMMO_OPTIONAL
  +NOALERT
  +INVENTORY.UNTOSSABLE
  Tag "Nuclear VBFG9000"
  Weapon.Kickback 40
  Weapon.SelectionOrder 3900
  Weapon.SlotNumber 7
  Weapon.AmmoUse1 60
  Weapon.AmmoGive1 0
  Weapon.AmmoType1 "RLNuclearVBFG9000Clip"
  States
  {
  Ready:
	TNT1 A 0 A_JumpIfInventory("RLRenegadeAssemblyCheck",1,"RenegadeAssemblyNoFire")
    TNT1 A 0 A_PlaySound("weapons/nukevbfg9000idle", 6, 1, 1)
	TNT1 A 0 A_JumpIfInventory("RLNuclearVBFG9000Clip", 60, "ReadyCantAltFire")
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop", 1, "ReadyCanAltFire")
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate", 1, "ReadyCanAltFire")
	TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000Charged",1)
    TNT1 A 0 A_GunFlash ("FlashAmmo", GFF_NOEXTCHANGE)
    DBFG A 5 A_WeaponReady (WRF_NOFIRE | WRF_ALLOWZOOM)
	Goto ReadyCycleCharging
  ReadyCanAltFire:
	TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000Charged",1)
    TNT1 A 0 A_GunFlash ("FlashAmmo", GFF_NOEXTCHANGE)
    DBFG A 5 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
	Goto ReadyCycleCharging
  ReadyCantAltFire:
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop", 1, "ReadyCanFire")
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate", 1, "ReadyCanFire")
	TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000Charged",1)
    TNT1 A 0 A_GunFlash ("FlashAmmo", GFF_NOEXTCHANGE)
    DBFG A 5 A_WeaponReady (WRF_NOSECONDARY | WRF_ALLOWZOOM)
	Goto ReadyCycleCharged
  ReadyCanFire:
    TNT1 A 0 A_GunFlash ("FlashAmmo", GFF_NOEXTCHANGE)
    DBFG A 5 A_WeaponReady (WRF_ALLOWZOOM)
	Goto ReadyCycleCharged
//=================================================================================
  ReadyCycleCharging:
	TNT1 A 0 A_JumpIfInventory("RLNuclearVBFG9000Clip", 60, "ReadyBeep")
	Goto ReadyChargingAmmo
  ReadyChargingAmmo:
	TNT1 A 0 A_GiveInventory("RLNuclearVBFG9000Clip", 1)
    TNT1 A 0 A_GunFlash ("FlashAmmo", GFF_NOEXTCHANGE)
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop", 1, "ReadyChargingAmmo2")
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate", 1, "ReadyChargingAmmo2")
	TNT1 A 0 A_JumpIfInventory("RLNuclearWeaponSetBonusActive", 1, 2)
    DBFG A 6 A_WeaponReady (WRF_NOFIRE | WRF_ALLOWZOOM)
    Goto ReadyCycleCharging
    DBFG A 3 A_WeaponReady (WRF_NOFIRE | WRF_ALLOWZOOM)
    Goto ReadyCycleCharging
  ReadyChargingAmmo2:
	TNT1 A 0 A_JumpIfInventory("RLNuclearWeaponSetBonusActive", 1, 2)
    DBFG A 6 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
    Goto ReadyCycleCharging
    DBFG A 3 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
    Goto ReadyCycleCharging
//=================================================================================
  ReadyCycleCharged:
	TNT1 A 0 A_JumpIfInventory("RLNuclearVBFG9000BulkMod",1,2)
	TNT1 A 0 A_JumpIfInventory("RLNuclearVBFG9000Clip", 60, "Ready")
	Goto ReadyChargedAmmo
	TNT1 A 0 A_JumpIfInventory("RLNuclearVBFG9000Clip", 78, "Ready")
	Goto ReadyChargedAmmo
  ReadyChargedAmmo:
	TNT1 A 0 A_GiveInventory("RLNuclearVBFG9000Clip", 1)
    TNT1 A 0 A_GunFlash ("FlashAmmo", GFF_NOEXTCHANGE)
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop", 1, "ReadyChargedAmmo2")
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate", 1, "ReadyChargedAmmo2")
	TNT1 A 0 A_JumpIfInventory("RLNuclearWeaponSetBonusActive", 1, 2)
    DBFG A 6 A_WeaponReady (WRF_NOSECONDARY | WRF_ALLOWZOOM)
    Goto ReadyCycleCharged
    DBFG A 3 A_WeaponReady (WRF_NOSECONDARY | WRF_ALLOWZOOM)
    Goto ReadyCycleCharged
  ReadyChargedAmmo2:
	TNT1 A 0 A_JumpIfInventory("RLNuclearWeaponSetBonusActive", 1, 2)
    DBFG A 6 A_WeaponReady (WRF_ALLOWZOOM)
    Goto ReadyCycleCharged
    DBFG A 3 A_WeaponReady (WRF_ALLOWZOOM)
    Goto ReadyCycleCharged
//=================================================================================
  ReadyBeep:
	TNT1 A 0 A_GiveInventory("RLNuclearVBFG9000Charged",1)
	TNT1 A 0 A_PlaySound("weapons/nukevbfg9000beep", 7)
	TNT1 A 0 A_JumpIfInventory("RLNuclearVBFG9000BulkMod",1,1)
	Goto ReadyBeep+4
	TNT1 A 0 A_GiveInventory("RLNuclearVBFG9000Clip", 1)
    TNT1 A 0 A_GunFlash ("FlashBeep", GFF_NOEXTCHANGE)
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop", 1, "ReadyBeep2")
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate", 1, "ReadyBeep2")
	TNT1 A 0 A_JumpIfInventory("RLNuclearWeaponSetBonusActive", 1, 2)
    DBFG B 6 A_WeaponReady (WRF_NOSECONDARY | WRF_ALLOWZOOM)
    Goto ReadyCycleCharged
    DBFG B 3 A_WeaponReady (WRF_NOSECONDARY | WRF_ALLOWZOOM)
    Goto ReadyCycleCharged
  ReadyBeep2:
	TNT1 A 0 A_JumpIfInventory("RLNuclearWeaponSetBonusActive", 1, 2)
    DBFG B 6 A_WeaponReady (WRF_ALLOWZOOM)
    Goto ReadyCycleCharged
    DBFG B 3 A_WeaponReady (WRF_ALLOWZOOM)
    Goto ReadyCycleCharged
  ScavengerDrop:
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"WeaponDropConfirm")
	TNT1 A 0 A_GiveInventory("RLScavengerDrop",1)
	TNT1 A 0 A_Print("Press again to drop current weapon, or press Alt Fire to scrap it for a Mod Pack.")
	Goto AfterModDrop
  WeaponDropConfirm:
	TNT1 A 0 A_JumpIfInventory("RLShellshockPerk",1, 1)
	Goto WeaponDropConfirm+2
	TNT1 A 0 A_GiveInventory("RLRenegadeAssemblyCheck",1)
	TNT1 A 0 A_JumpIfInventory("RLNuclearPlasmaPistol",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLHighPowerNuclearPlasmaPistol",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLStormNuclearPlasmaPistol",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLSuperchargedNuclearPlasmaPistol",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLNuclearPlasmaRifle",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLAssaultRifleNuclearPlasmaRifle",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonNuclearPlasmaRifle",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLHighPowerNuclearPlasmaRifle",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLNuclearBFG9000",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLHighPowerNuclearBFG9000",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLNuclearBiggestFuckingGun",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_TakeInventory("RLNuclearWeaponSetBonusWeapon", 1)
	TNT1 A 0 A_TakeInventory("RLNuclearWeaponSetBonusActive", 1)
  WeaponDropConfirmContinue:
	TNT1 A 0 A_StopSound(6)
    TNT1 A 0 A_GunFlash ("FlashDrop", GFF_NOEXTCHANGE)
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
    TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000ModLimit", 1)
    TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000PowerMod", 1)
    TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000BulkMod", 1)
    TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000AgilityMod", 1)
    TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000FirestormMod", 1)
    TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000SniperMod", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000Selected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
	TNT1 A 0 A_FireCustomMissile("DroppedRLNuclearVBFG9000",0,0,0)
	TNT1 A 1 A_TakeInventory("RLNuclearVBFG9000",1)
	Stop
  AfterModDrop:
    TNT1 A 0 A_GunFlash ("FlashAmmo", GFF_NOEXTCHANGE)
    DBFG A 5 A_WeaponReady (16)
    TNT1 A 0 A_GunFlash ("FlashAmmo", GFF_NOEXTCHANGE)
    DBFG A 3 A_WeaponReady (16)
    Goto Ready
  RenegadeAssemblyNoFire:
	TNT1 A 0 A_TakeInventory("RLRenegadeAssemblyCheck",1)
    TNT1 A 0 A_GunFlash ("FlashAmmo", GFF_NOEXTCHANGE)
    DBFG A 5 A_WeaponReady (WRF_NOFIRE | WRF_NOBOB)
    TNT1 A 0 A_GunFlash ("FlashAmmo", GFF_NOEXTCHANGE)
    DBFG A 5 A_WeaponReady (WRF_NOFIRE | WRF_NOBOB)
    Goto Ready
  Deselect:
	TNT1 A 0
    TNT1 A 0 A_StopSound(CHAN_WEAPON)
    TNT1 A 0 A_StopSound(6)
    TNT1 A 0 A_GunFlash ("FlashDrop", GFF_NOEXTCHANGE)
	TNT1 A 0 A_TakeInventory("RLArmorModItemActivate",1)
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
	TNT1 A 0 A_TakeInventory("RLUseBulkMod",1)
	TNT1 A 0 A_TakeInventory("RLUsePowerMod",1)
	TNT1 A 0 A_TakeInventory("RLUseAgilityMod",1)
	TNT1 A 0 A_TakeInventory("RLUseTechnicalMod",1)
	TNT1 A 0 A_TakeInventory("RLUseNanoMod",1)
	TNT1 A 0 A_TakeInventory("RLUseFirestormMod",1)
	TNT1 A 0 A_TakeInventory("RLUseSniperMod",1)
	TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000Selected", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
  DeselectLoop:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "DeselectRenegade")
    DBFG A 2 A_Lower
    Loop
  DeselectRenegade:
    TNT1 A 0 A_Lower
    loop
  SelectRenegade: 
    TNT1 A 0 A_Raise
    loop
  SelectSetWeapon:
	TNT1 A 0 A_GiveInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_GiveInventory("RLNuclearVBFG9000Selected", 1)
    Goto SelectLoop
  SelectLoop:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "SelectRenegade")
    DBFG A 2 A_Raise
    Loop
  Fire:
	TNT1 A 0 A_JumpIfNoAmmo("Ready")
	TNT1 A 0 A_AlertMonsters
	TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000Charged",1)
    DBFG B 10 A_PlaySound("weapons/nukevbfg9000charge", CHAN_WEAPON)
	TNT1 A 0 A_GunFlash
	DBFC ABCDE 2 Bright
	DBFC FGHIJFGHIJFGHIJFGHIJFGHIJFGHIJ 2 Bright
	DBFC FGHIJFGHIJFGHIJFGH 1 Bright
	TNT1 A 0 A_JumpIfInventory("RLNuclearVBFG9000FirestormMod",1,"FireF")
	TNT1 A 0 A_JumpIfInventory("RLNuclearVBFG9000SniperMod",1,"FireS")
	TNT1 A 0 A_JumpIfInventory("RLNuclearVBFG9000PowerMod",1,"FireP")
	TNT1 A 0 A_JumpIfInventory("RLNuclearVBFG9000AgilityMod",1,"FireA")
	TNT1 A 0 A_FireCustomMissile ("RLVBFGBall", frandom(1,-1), 1)
    Goto FireFinish
  FireP:
	TNT1 A 0 A_FireCustomMissile ("RLVBFGBallP", frandom(1,-1), 1)
    Goto FireFinish
  FireA:
	TNT1 A 0 A_FireCustomMissile ("RLVBFGBallA", frandom(1,-1), 1)
    Goto FireFinish
  FireS:
	TNT1 A 0 A_FireCustomMissile ("RLVBFGBallS", 0, 1)
    Goto FireFinish
  FireF:
	TNT1 A 0 A_FireCustomMissile ("RLVBFGBallF", frandom(1,-1), 1)
    Goto FireFinish
  FireFinish:
    DBFF AB 2 Bright
    DBFG CDA 4 Bright
    DBFG A 20 A_Refire
    Goto Ready
  Flash:
	NVBX EGEEG 2 Bright A_Light(random(1,4))
	NVBX EGEEGEGEEGEGEEGEGEEGEGEEGEGEEG 2 Bright A_Light(random(1,4))
	NVBX EGEEGEGEEGEGEEGEGE 1 Bright A_Light(random(1,4))
	TNT1 A 1 A_Light(8)
	TNT1 A 1 A_Light(6)
	TNT1 A 1 A_Light(4)
	TNT1 A 1 A_Light(2)
	Goto LightDone
  FlashAmmo:
	TNT1 A 0 A_JumpIfInventory("RLNuclearVBFG9000Clip", 60, 9)
	TNT1 A 0 A_JumpIfInventory("RLNuclearVBFG9000Clip", 48, 7)
	TNT1 A 0 A_JumpIfInventory("RLNuclearVBFG9000Clip", 36, 5)
	TNT1 A 0 A_JumpIfInventory("RLNuclearVBFG9000Clip", 24, 3)
	TNT1 A 0 A_JumpIfInventory("RLNuclearVBFG9000Clip", 12, 1)
    Goto LightDone
    NVBX A 6 Bright
    Goto LightDone
    NVBX B 6 Bright
    Goto LightDone
    NVBX C 6 Bright
    Goto LightDone
    NVBX D 6 Bright
    Goto LightDone
    NVBX E 6 Bright
    Goto LightDone
  FlashBeep:
    NVBX F 6 Bright
    Goto LightDone
  FlashDrop:
    TNT1 A 0
    Goto LightDone
  Spawn:
    BFUG A -1
    Stop
  Altfire:
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate",1, "ArmorModConversion")
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"ScavengerTime")
	Goto Ready
  ScavengerTime:
	TNT1 A 0 A_Print("Mod pack extracted from discarded weapon.")
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
    TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000ModLimit", 1)
    TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000PowerMod", 1)
    TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000BulkMod", 1)
    TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000AgilityMod", 1)
    TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000SniperMod", 1)
    TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000FirestormMod", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLNuclearVBFG9000Selected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
    TNT1 A 0 A_StopSound(6)
    TNT1 A 0 A_GunFlash ("FlashDrop", GFF_NOEXTCHANGE)
	TNT1 A 0 A_JumpIfInventory("RLNuclearPlasmaPistol",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLHighPowerNuclearPlasmaPistol",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLStormNuclearPlasmaPistol",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLSuperchargedNuclearPlasmaPistol",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLNuclearPlasmaRifle",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLAssaultRifleNuclearPlasmaRifle",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonNuclearPlasmaRifle",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLHighPowerNuclearPlasmaRifle",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLNuclearBFG9000",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLHighPowerNuclearBFG9000",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLNuclearBiggestFuckingGun",1,"ScavengerContinue")
	TNT1 A 0 A_TakeInventory("RLNuclearWeaponSetBonusWeapon", 1)
	TNT1 A 0 A_TakeInventory("RLNuclearWeaponSetBonusActive", 1)
  ScavengerContinue:
	TNT1 A 0 A_FireCustomMissile("DroppedRLPowerMod",0,0,0)
	Goto ScavengerTimeEnd
  ScavengerTimeEnd:
	TNT1 A 1 A_TakeInventory("RLNuclearVBFG9000",1)
	Stop
  }
}

//-------------------------------------------------------------------
//		MECHANICS

Actor RLNuclearVBFG9000Selected : Inventory
{
inventory.maxamount 1
}

Actor RLNuclearVBFG9000Charged : Inventory
{
inventory.maxamount 1
}

Actor RLNuclearVBFG9000ModLimit : Inventory
{
inventory.maxamount 1
}

Actor RLNuclearVBFG9000PowerMod : Inventory
{
inventory.maxamount 1
}

Actor RLNuclearVBFG9000BulkMod : Inventory
{
inventory.maxamount 1
}

Actor RLNuclearVBFG9000AgilityMod : Inventory
{
inventory.maxamount 1
}

Actor RLNuclearVBFG9000FirestormMod : Inventory
{
inventory.maxamount 1
}

Actor RLNuclearVBFG9000SniperMod : Inventory
{
inventory.maxamount 1
}

ACTOR RLNuclearVBFG9000Clip : Ammo
{
   +INVENTORY.IGNORESKILL
   Inventory.MaxAmount 78
   Inventory.Icon ""
}

ACTOR RLNuclearVBFG9000Pickup : CustomInventory
{
	Radius 16
	Height 8
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
	Spawn:
		DEBF A -1
		Stop
	Pickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("RLNuclearVBFG9000",1,"AmmoPickup") // If the player already has this weapon, they get more ammo for it
		TNT1 A 0 A_JumpIfInventory("RLWeaponLimit",0,"NotPickup") // If the player has max weapons, they get nothing
		TNT1 A 0 A_PlaySound("weapons/nukevbfg9000pickup")
		TNT1 A 0 A_GiveInventory("RLNuclearVBFG9000",1)
		TNT1 A 0 A_GiveInventory("RLWeaponLimit",1)
		TNT1 A 0 A_GiveInventory("RLNuclearWeaponSetBonusWeapon",1)
		TNT1 A 0 A_JumpIfInventory("RLNuclearWeaponSetBonusArmor",1,2)
		TNT1 A 0 A_Print("Picked up a \cvNuclear VBFG 9000. \c-Oh yes.")
		Stop
		TNT1 A 0 A_GiveInventory("RLNuclearWeaponSetBonusActive",1)
		TNT1 A 0 A_Print("Picked up a \cvNuclear VBFG 9000.\n\c-= FURIOUS FUSION =\n\cgNuclear weaponry recharges twice as fast", 8, "CONFONT")
		Stop
	NotPickup:
		TNT1 A 0 A_JumpIfInventory("DRLA_WeaponAntiLogFloodCheck",1,"NotPickup_NoPrint")
		TNT1 A 0 A_GiveInventory("DRLA_WeaponAntiLogFloodCheck")
		TNT1 A 0 A_Print("\cv= Nuclear VBFG 9000 =", 2, "CONFONT")
	 NotPickup_NoPrint:	
		TNT1 A 0
		Fail
	AmmoPickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("Cell",0,"AmmoFull")
		TNT1 A 0 A_PlaySound("misc/w_pkup")
		TNT1 A 1 A_GiveInventory("Cell",60)
		Stop
	AmmoFull:
		DEBF A 1
		Fail
	}
}

ACTOR DroppedRLNuclearVBFG9000
{
	PROJECTILE
	-NOGRAVITY
	Damage 0
	Radius 16
	Height 8
	Speed 9
	DeathSound "weapons/gundrop"
	States
	{
	Spawn:
		DEBF A 1
		Loop
	Death:
		TNT1 A 0
		DEBF A 1 A_SpawnItem("RLNuclearVBFG9000Pickup",0,0,0,0)
		Stop
	}
}
