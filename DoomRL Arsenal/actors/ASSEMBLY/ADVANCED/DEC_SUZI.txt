
ACTOR RLStormUzi : RLWeapon
{
  Inventory.Pickupsound "misc/w_pkup"
  Weapon.UpSound "weapons/weaponholster"
  Obituary "%o was no match for %k's uzis."
  Inventory.Pickupmessage "You picked up dual uzis."
  Decal BulletChip
  +AMMO_OPTIONAL
  +NOALERT
  +INVENTORY.UNTOSSABLE
  Tag "Dual Uzis"
  Weapon.Kickback 40
  Weapon.SelectionOrder 25
  Weapon.SlotNumber 4
  Weapon.AmmoUse1 1
  Weapon.AmmoGive1 0
  Weapon.AmmoType1 "RLStormUziClip"
  Weapon.AmmoType2 "Clip"
  States
  {
  Ready:
	TNT1 A 0 A_JumpIfInventory("RLRenegadeAssemblyCheck",1,"RenegadeAssemblyNoFire")
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "ReadyMain")
    SUZI SRQPONMLKJ 1
    Goto ReadyMain
  ReadyMain:
	TNT1 A 0 A_JumpIfInventory("RLStormUziNanoMod",1,"NanoRegenCheck")
    SUZI A 1 A_WeaponReady (48)
    Goto ReadyMain
  NanoRegenCheck:
	TNT1 A 0 A_JumpIfInventory("RLStormUziClip",1,2)
    SUZI A 70 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
	Goto NanoRegenCheckBegin
  NanoRegenCheckBegin:
    SUZI A 70 A_WeaponReady (32)
	TNT1 A 0 A_JumpIfInventory("RLStormUziBulkMod",1,2)
	TNT1 A 0 A_JumpIfInventory("RLStormUziClip", 64, "ReadyMain")
	Goto NanoRegen
	TNT1 A 0 A_JumpIfInventory("RLStormUziClip", 84, "ReadyMain")
	Goto NanoRegen
  NanoRegen:
	TNT1 A 0 A_GiveInventory("RLStormUziClip", 1)
    SUZI A 17 A_WeaponReady (32)
    Goto NanoRegenCheckBegin+1
  ScavengerDrop:
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"WeaponDropConfirm")
	TNT1 A 0 A_GiveInventory("RLScavengerDrop",1)
	TNT1 A 0 A_Print("Press again to drop current weapon, or press Alt Fire to scrap it for a Mod Pack.")
	Goto AfterModDrop
  WeaponDropConfirm:
	TNT1 A 0 A_JumpIfInventory("RLShellshockPerk",1, 1)
	Goto WeaponDropConfirm+2
	TNT1 A 0 A_GiveInventory("RLRenegadeAssemblyCheck",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
    TNT1 A 0 A_TakeInventory("RLStormUziModLimit", 1)
    TNT1 A 0 A_TakeInventory("RLStormUziPowerMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormUziBulkMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormUziAgilityMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormUziTechnicalMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormUziFirestormMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormUziSniperMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormUziNanoMod", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLStormUziSelected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
	TNT1 A 0 A_FireCustomMissile("DroppedRLStormUzi",0,0,0)
	TNT1 A 1 A_TakeInventory("RLStormUzi",1)
	Stop
  AfterModDrop:
    SUZI A 8 A_WeaponReady (16)
    Goto ReadyMain
  RenegadeAssemblyNoFire:
	TNT1 A 0 A_TakeInventory("RLRenegadeAssemblyCheck",1)
    SUZI A 9 A_WeaponReady (WRF_NOFIRE | WRF_NOBOB)
    Goto ReadyMain
  Deselect:
	TNT1 A 0
	TNT1 A 0 A_TakeInventory("RLArmorModItemActivate",1)
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
	TNT1 A 0 A_TakeInventory("RLUseBulkMod",1)
	TNT1 A 0 A_TakeInventory("RLUsePowerMod",1)
	TNT1 A 0 A_TakeInventory("RLUseAgilityMod",1)
	TNT1 A 0 A_TakeInventory("RLUseTechnicalMod",1)
	TNT1 A 0 A_TakeInventory("RLUseNanoMod",1)
	TNT1 A 0 A_TakeInventory("RLUseFirestormMod",1)
	TNT1 A 0 A_TakeInventory("RLUseSniperMod",1)
	TNT1 A 0 A_TakeInventory("RLStormUziSelected", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "DeselectLoop")
    SUZI JKLMNOPQRS 1
  DeselectLoop:
    TNT1 A 0 A_Lower
    Loop
  SelectSetWeapon:
	TNT1 A 0 A_GiveInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_GiveInventory("RLStormUziSelected", 1)
    Goto SelectLoop
  SelectLoop:
    TNT1 A 0 A_Raise
    Loop
  Fire:
	TNT1 A 0 A_JumpIfNoAmmo("Reload")
	TNT1 A 0 A_AlertMonsters
	TNT1 A 0 A_GiveInventory("RLStormUziAmmochain",1)
	TNT1 A 0 A_JumpIfInventory("RLStormUziFirestormMod",1,"FireF")
	TNT1 A 0 A_JumpIfInventory("RLStormUziSniperMod",1,"FireS")
	TNT1 A 0 A_JumpIfInventory("RLStormUziPowerMod",1,"FireP")
	TNT1 A 0 A_JumpIfInventory("RLStormUziAgilityMod",1,"FireA")
	TNT1 A 0 A_JumpIfInventory("RLSharpshooterPerk",1,5)
	TNT1 A 0 A_JumpIfInventory("RLStormUziAmmochain",4,2)
	TNT1 A 0 A_JumpIfInventory("RLAmmochainPerk",1,2)
	TNT1 A 0 A_FireBullets (9, 6, -1, 4, "RLBulletPuff")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (9, 6, -1, 4, "RLBulletPuff", "None")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (9, 6, -1, (32), "RLBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
    Goto FireFinish
  FireP:
	TNT1 A 0 A_JumpIfInventory("RLSharpshooterPerk",1,5)
	TNT1 A 0 A_JumpIfInventory("RLStormUziAmmochain",4,2)
	TNT1 A 0 A_JumpIfInventory("RLAmmochainPerk",1,2)
	TNT1 A 0 A_FireBullets (9, 6, -1, 5, "RLBulletPuff")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (9, 6, -1, 5, "RLBulletPuff", "None")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (9, 6, -1, (40), "RLBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
    Goto FireFinish
  FireA:
	TNT1 A 0 A_JumpIfInventory("RLSharpshooterPerk",1,5)
	TNT1 A 0 A_JumpIfInventory("RLStormUziAmmochain",4,2)
	TNT1 A 0 A_JumpIfInventory("RLAmmochainPerk",1,2)
	TNT1 A 0 A_FireBullets (8.5, 5.5, -1, 4, "RLBulletPuff")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (8.5, 5.5, -1, 4, "RLBulletPuff", "None")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (8.5, 5.5, -1, (32), "RLBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
    Goto FireFinish
  FireS:
	TNT1 A 0 A_JumpIfInventory("RLSharpshooterPerk",1,5)
	TNT1 A 0 A_JumpIfInventory("RLStormUziAmmochain",4,2)
	TNT1 A 0 A_JumpIfInventory("RLAmmochainPerk",1,2)
	TNT1 A 0 A_FireBullets (7, 4, 1, 4, "RLBulletPuff")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (7, 4, 1, 4, "RLBulletPuff", "None")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (7, 4, 1, (32), "RLBulletPuff", FBF_NORANDOM | FBF_USEAMMO)
    Goto FireFinish
  FireF:
	TNT1 A 0 A_JumpIfInventory("RLSharpshooterPerk",1,5)
	TNT1 A 0 A_JumpIfInventory("RLStormUziAmmochain",4,2)
	TNT1 A 0 A_JumpIfInventory("RLAmmochainPerk",1,2)
	TNT1 A 0 A_FireBullets (9, 6, -1, 5, "RLShotgunPuffF")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (9, 6, -1, 5, "RLShotgunPuffF", "None")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (9, 6, -1, (40), "RLShotgunPuffF", FBF_NORANDOM | FBF_USEAMMO)
    Goto FireFinish
  FireFinish:
	TNT1 A 0 A_JumpIfInventory("RLStormUziContinueFire",1,"FireFinishBoth")
	TNT1 A 0 A_JumpIfInventory("RLStormUziState",1,"FireFinishLeft")
    TNT1 A 0 A_PlaySound("weapons/stormuzi", CHAN_WEAPON)
    SUZI B 1 Bright A_GunFlash
	TNT1 A 0 A_GiveInventory("RLStormUziState",1)
	TNT1 A 0 A_Refire("FireReturn")
	TNT1 A 0 A_TakeInventory("RLStormUziContinueFire",1)
    SUZI D 1 Bright
    SUZI H 1
    Goto FireFinishAmmochain
  FireFinishLeft:
    TNT1 A 0 A_PlaySound("weapons/stormuzi", CHAN_WEAPON)
    SUZI C 1 Bright A_GunFlash
	TNT1 A 0 A_TakeInventory("RLStormUziState",1)
	TNT1 A 0 A_Refire("FireReturn")
	TNT1 A 0 A_TakeInventory("RLStormUziContinueFire",1)
    SUZI E 1 Bright
    SUZI I 1
    Goto FireFinishAmmochain
  FireFinishBoth:
	TNT1 A 0 A_JumpIfInventory("RLStormUziState",1,"FireFinishBothLeft")
    TNT1 A 0 A_PlaySound("weapons/stormuzi", CHAN_WEAPON)
    SUZI G 1 Bright A_GunFlash
	TNT1 A 0 A_GiveInventory("RLStormUziState",1)
	TNT1 A 0 A_Refire("FireReturn")
	TNT1 A 0 A_TakeInventory("RLStormUziContinueFire",1)
    SUZI F 1 Bright
    SUZI E 1
    Goto FireFinishAmmochain
  FireFinishBothLeft:
    TNT1 A 0 A_PlaySound("weapons/stormuzi", 5)
    SUZI F 1 Bright A_GunFlash
	TNT1 A 0 A_TakeInventory("RLStormUziState",1)
	TNT1 A 0 A_Refire("FireReturn")
	TNT1 A 0 A_TakeInventory("RLStormUziContinueFire",1)
    SUZI G 1 Bright
    SUZI D 1
    Goto FireFinishAmmochain
  FireReturn:
	TNT1 A 0 A_GiveInventory("RLStormUziContinueFire",1)
	TNT1 A 0 A_JumpIfInventory("RLStormUziAmmochain",4,2)
	TNT1 A 0 A_Jump(256,2)
	TNT1 A 0 A_TakeInventory("RLStormUziAmmochain",4)
	TNT1 A 0
    Goto Fire
  FireFinishAmmochain:
	TNT1 A 0 A_JumpIfInventory("RLStormUziAmmochain",4,2)
	TNT1 A 0 A_Jump(256,2)
	TNT1 A 0 A_TakeInventory("RLStormUziAmmochain",4)
	TNT1 A 0
    Goto ReadyMain
  Reload:
	TNT1 A 0 A_JumpIfInventory("RLStormUziNanoMod",1,"ReadyMain")
	SUZI A 1 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
	TNT1 A 0 A_JumpIfInventory("RLStormUziBulkMod",1,2)
	TNT1 A 0 A_JumpIfInventory("RLStormUziClip", 64, "ReadyMain")
	Goto Reload+5
	TNT1 A 0 A_JumpIfInventory("RLStormUziClip", 84, "ReadyMain")
	TNT1 A 0 A_JumpIfInventory("Clip", 1, 1)
	Goto ReadyMain
	TNT1 A 0 // Play the reload animation.
	TNT1 A 0 A_JumpIfInventory("RLStormUziTechnicalMod",1,"Reload1")
    SUZI TUV 1
    TNT1 A 0 A_PlaySound("weapons/stormuziunload", 5)
    DUZR A 1
    TNT1 A 0 A_PlaySound("weapons/stormuziunload", 7)
    DUZR BCDEFGHIJKLMNO 2
	Goto ReloadWorking
  Reload1:
    SUZI TUV 1
    TNT1 A 0 A_PlaySound("weapons/stormuziunload", 5)
    DUZR A 1
    TNT1 A 0 A_PlaySound("weapons/stormuziunload", 7)
    DUZR BCD 1
    DUZR EFGHIJKLMNO 2
	Goto ReloadWorking
  ReloadWorking:
	TNT1 A 0 A_TakeInventory("Clip", 1)
	TNT1 A 0 A_GiveInventory("RLStormUziClip", 1)
	TNT1 A 0 A_JumpIfInventory("RLStormUziBulkMod",1,2)
	TNT1 A 0 A_JumpIfInventory("RLStormUziClip",64,3)
	Goto ReloadWorking+5
	TNT1 A 0 A_JumpIfInventory("RLStormUziClip",84,2)
	TNT1 A 0 A_JumpIfInventory("Clip",1,2)
	TNT1 A 0
	Goto ReloadFinish
	TNT1 A 0
	Goto ReloadWorking
  ReloadFinish:
    TNT1 A 0 A_PlaySound("weapons/stormuziload", 5)
	TNT1 A 0 A_JumpIfInventory("RLStormUziTechnicalMod",1,"ReloadFinish1")
    DUZR PQRSTUVWXYZ 2
    TNT1 A 0 A_PlaySound("weapons/stormuziload", 7)
    DUZ2 ABCDEFG 2
    SUZI A 2
	Goto ReadyMain
  ReloadFinish1:
    DUZR PQRSTUVWXYZ 2
    TNT1 A 0 A_PlaySound("weapons/stormuziload", 7)
    DUZ2 ABCDE 2
    DUZ2 FG 1
    SUZI A 1
	Goto ReadyMain
  Flash:
    TNT1 A 1 A_Light2
    TNT1 A 1 A_Light1
    Goto LightDone
  Spawn:
    SUZI X -1
    Stop
  Altfire:
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate",1, "ArmorModConversion")
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"ScavengerTime")
	Goto ReadyMain
  ScavengerTime:
	TNT1 A 0 A_Print("Mod pack extracted from discarded weapon.")
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
    TNT1 A 0 A_TakeInventory("RLStormUziModLimit", 1)
    TNT1 A 0 A_TakeInventory("RLStormUziPowerMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormUziBulkMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormUziAgilityMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormUziTechnicalMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormUziSniperMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormUziFirestormMod", 1)
    TNT1 A 0 A_TakeInventory("RLStormUziNanoMod", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLStormUziSelected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
    TNT1 A 0 A_StopSound(6)
	TNT1 A 0 A_Jump(256,1,2,3)
	TNT1 A 0 A_FireCustomMissile("DroppedRLBulkMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLBulkMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLTechnicalMod",0,0,0)
	Goto ScavengerTimeEnd
  ScavengerTimeEnd:
	TNT1 A 1 A_TakeInventory("RLStormUzi",1)
	Stop
  }
}

//-------------------------------------------------------------------
//		MECHANICS

Actor RLStormUziSelected : Inventory
{
inventory.maxamount 1
}

Actor RLStormUziAmmochain : Inventory
{
inventory.maxamount 4
}

Actor RLStormUziState : Inventory
{
inventory.maxamount 1
}

Actor RLStormUziContinueFire : Inventory
{
inventory.maxamount 1
}

Actor RLStormUziModLimit : Inventory
{
inventory.maxamount 1
}

Actor RLStormUziPowerMod : Inventory
{
inventory.maxamount 1
}

Actor RLStormUziBulkMod : Inventory
{
inventory.maxamount 1
}

Actor RLStormUziAgilityMod : Inventory
{
inventory.maxamount 1
}

Actor RLStormUziTechnicalMod : Inventory
{
inventory.maxamount 1
}

Actor RLStormUziFirestormMod : Inventory
{
inventory.maxamount 1
}

Actor RLStormUziNanoMod : Inventory
{
inventory.maxamount 1
}

Actor RLStormUziSniperMod : Inventory
{
inventory.maxamount 1
}

ACTOR RLStormUziClip : Ammo
{
   +INVENTORY.IGNORESKILL
   Inventory.MaxAmount 84
   Inventory.Icon ""
}

ACTOR RLStormUziPickup : CustomInventory
{
	Radius 16
	Height 8
	Scale 0.5
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
	Spawn:
		SUZI X -1
		Stop
	Pickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("RLStormUzi",1,"NotPickup") // If the player already has this weapon, they are not given the weapon
		TNT1 A 0 A_JumpIfInventory("RLWeaponLimit",0,"NotPickup2") // The absolute max limit check for weapons
		TNT1 A 0 A_PlaySound("weapons/stormuzipickup")
		TNT1 A 0 A_GiveInventory("RLStormUzi",1)
		TNT1 A 0 A_GiveInventory("RLWeaponLimit",1)
		TNT1 A 0 A_Print("Picked up \cvDual Uzis.")
		Stop
	NotPickup:
		TNT1 A 0 A_JumpIfInventory("Clip",0,1)
		Goto AmmoPickup
	NotPickup2:
		TNT1 A 0 A_JumpIfInventory("DRLA_WeaponAntiLogFloodCheck",1,"NotPickup_NoPrint")
		TNT1 A 0 A_GiveInventory("DRLA_WeaponAntiLogFloodCheck")
		TNT1 A 0 A_Print("\cv= Dual Uzis =", 2, "CONFONT")
	 NotPickup_NoPrint:	
		TNT1 A 0
		Fail
	AmmoPickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("Clip",0,"AmmoFull")
		TNT1 A 0 A_PlaySound("misc/w_pkup")
		TNT1 A 1 A_GiveInventory("Clip",64)
		Stop
	AmmoFull:
		SUZI X 1
		Fail
	}
}

ACTOR DroppedRLStormUzi
{
	PROJECTILE
	-NOGRAVITY
	Damage 0
	Radius 16
	Height 8
	Speed 9
	Scale 0.5
	DeathSound "weapons/gundrop"
	States
	{
	Spawn:
		SUZI X 1
		Loop
	Death:
		TNT1 A 0
		SUZI X 1 A_SpawnItem("RLStormUziPickup",0,0,0,0)
		Stop
	}
}
