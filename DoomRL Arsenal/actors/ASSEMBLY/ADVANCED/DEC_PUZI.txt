
ACTOR RLPlasmaUzi : RLWeapon
{
  Inventory.Pickupsound "misc/w_pkup"
  Weapon.UpSound "weapons/weaponholster"
  Obituary "%o was burnt a lot by %k's plasma uzi."
  Inventory.Pickupmessage "You picked up a plasma uzi."
  Decal BulletChip
  +AMMO_OPTIONAL
  +NOALERT
  +INVENTORY.UNTOSSABLE
  Tag "Plasma Uzi"
  Weapon.Kickback 40
  Weapon.SelectionOrder 25
  Weapon.SlotNumber 6
  Weapon.AmmoUse1 1
  Weapon.AmmoGive1 0
  Weapon.AmmoType1 "RLPlasmaUziClip"
  Weapon.AmmoType2 "Cell"
  States
  {
  Ready:
	TNT1 A 0 A_JumpIfInventory("RLRenegadeAssemblyCheck",1,"RenegadeAssemblyNoFire")
    TNT1 A 0 A_PlaySound("weapons/plasmauziidle", 6, 1, 1)
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziNanoMod",1,2)
    PUZI BCD 1 A_WeaponReady (48)
    Goto Ready
  NanoRegenCheck:
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziClip",1,"NanoRegenCheckBegin")
    PUZI BCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCD 1 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
	Goto ReadyNanoNormal
  ReadyNanoNormal:
    PUZI BCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCDBCD 1 A_WeaponReady (32)
  ReadyNano2:
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziClip", 32, "Ready")
	TNT1 A 0 A_GiveInventory("RLPlasmaUziClip", 1)
    PUZI BCDBCDBCDBCDBCD 1 A_WeaponReady (32)
    Goto ReadyNano2
  ScavengerDrop:
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"WeaponDropConfirm")
	TNT1 A 0 A_GiveInventory("RLScavengerDrop",1)
	TNT1 A 0 A_Print("Press again to drop current weapon, or press Alt Fire to scrap it for a Mod Pack.")
	Goto AfterModDrop
  WeaponDropConfirm:
	TNT1 A 0 A_JumpIfInventory("RLShellshockPerk",1, 1)
	Goto WeaponDropConfirm+2
	TNT1 A 0 A_GiveInventory("RLRenegadeAssemblyCheck",1)
    TNT1 A 0 A_StopSound(6)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
    TNT1 A 0 A_TakeInventory("RLPlasmaUziModLimit", 1)
    TNT1 A 0 A_TakeInventory("RLPlasmaUziPowerMod", 1)
    TNT1 A 0 A_TakeInventory("RLPlasmaUziBulkMod", 1)
    TNT1 A 0 A_TakeInventory("RLPlasmaUziAgilityMod", 1)
    TNT1 A 0 A_TakeInventory("RLPlasmaUziTechnicalMod", 1)
    TNT1 A 0 A_TakeInventory("RLPlasmaUziFirestormMod", 1)
    TNT1 A 0 A_TakeInventory("RLPlasmaUziSniperMod", 1)
    TNT1 A 0 A_TakeInventory("RLPlasmaUziNanoMod", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLPlasmaUziSelected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
	TNT1 A 0 A_FireCustomMissile("DroppedRLPlasmaUzi",0,0,0)
	TNT1 A 1 A_TakeInventory("RLPlasmaUzi",1)
	Stop
  AfterModDrop:
    TNT1 A 0 A_PlaySound("weapons/plasmauziidle", 6, 1, 1)
    PUZI BCDBCDBCD 1 A_WeaponReady (16)
    Goto Ready
  RenegadeAssemblyNoFire:
	TNT1 A 0 A_TakeInventory("RLRenegadeAssemblyCheck",1)
    TNT1 A 0 A_PlaySound("weapons/plasmauziidle", 6, 1, 1)
    PUZI BCDBCDBCD 1 A_WeaponReady (WRF_NOPRIMARY | WRF_NOBOB)
    Goto Ready
  Deselect:
	TNT1 A 0
    TNT1 A 0 A_StopSound(6)
	TNT1 A 0 A_TakeInventory("RLArmorModItemActivate",1)
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
	TNT1 A 0 A_TakeInventory("RLUseBulkMod",1)
	TNT1 A 0 A_TakeInventory("RLUsePowerMod",1)
	TNT1 A 0 A_TakeInventory("RLUseAgilityMod",1)
	TNT1 A 0 A_TakeInventory("RLUseTechnicalMod",1)
	TNT1 A 0 A_TakeInventory("RLUseNanoMod",1)
	TNT1 A 0 A_TakeInventory("RLUseFirestormMod",1)
	TNT1 A 0 A_TakeInventory("RLUseSniperMod",1)
	TNT1 A 0 A_TakeInventory("RLPlasmaUziSelected", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
  DeselectLoop:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "DeselectRenegade")
    TNT1 A 0 A_Lower
    PUZI A 1 A_Lower
    Loop
  DeselectRenegade:
    TNT1 A 0 A_Lower
    loop
  SelectRenegade: 
    TNT1 A 0 A_Raise
    loop
  SelectSetWeapon:
	TNT1 A 0 A_GiveInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_GiveInventory("RLPlasmaUziSelected", 1)
    Goto SelectLoop
  SelectLoop:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "SelectRenegade")
    TNT1 A 0 A_Raise
    PUZI A 1 A_Raise
    Loop
  Fire:
	TNT1 A 0 A_JumpIfNoAmmo("Reload")
	TNT1 A 0 A_AlertMonsters
	TNT1 A 0 A_GiveInventory("RLPlasmaUziAmmochain",1)
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziSniperMod",1,"FireS")
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziPowerMod",1,"FireP")
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziAgilityMod",1,"FireA")
	TNT1 A 0 A_JumpIfInventory("RLSharpshooterPerk",1,5)
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziAmmochain",4,2)
	TNT1 A 0 A_JumpIfInventory("RLAmmochainPerk",1,2)
	TNT1 A 0 A_FireCustomMissile ("RLPlasmaUziBall", frandom(5,-5), 1, 0, 0, 0, frandom(2,-2))
    Goto FireFinish
	TNT1 A 0 A_FireCustomMissile ("RLPlasmaUziBall", frandom(5,-5), 0, 0, 0, 0, frandom(2,-2))
    Goto FireFinish
	TNT1 A 0 A_FireCustomMissile ("RLPlasmaUziBallSharpshooter", frandom(5,-5), 1, 0, 0, 0, frandom(2,-2))
    Goto FireFinish
  FireP:
	TNT1 A 0 A_JumpIfInventory("RLSharpshooterPerk",1,5)
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziAmmochain",4,2)
	TNT1 A 0 A_JumpIfInventory("RLAmmochainPerk",1,2)
	TNT1 A 0 A_FireCustomMissile ("RLPlasmaUziBallP", frandom(5,-5), 1, 0, 0, 0, frandom(2,-2))
    Goto FireFinish
	TNT1 A 0 A_FireCustomMissile ("RLPlasmaUziBallP", frandom(5,-5), 0, 0, 0, 0, frandom(2,-2))
    Goto FireFinish
	TNT1 A 0 A_FireCustomMissile ("RLPlasmaUziBallPSharpshooter", frandom(5,-5), 1, 0, 0, 0, frandom(2,-2))
    Goto FireFinish
  FireA:
	TNT1 A 0 A_JumpIfInventory("RLSharpshooterPerk",1,5)
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziAmmochain",4,2)
	TNT1 A 0 A_JumpIfInventory("RLAmmochainPerk",1,2)
	TNT1 A 0 A_FireCustomMissile ("RLPlasmaUziBallA", frandom(5,-5), 1, 0, 0, 0, frandom(2,-2))
    Goto FireFinish
	TNT1 A 0 A_FireCustomMissile ("RLPlasmaUziBallA", frandom(5,-5), 0, 0, 0, 0, frandom(2,-2))
    Goto FireFinish
	TNT1 A 0 A_FireCustomMissile ("RLPlasmaUziBallASharpshooter", frandom(5,-5), 1, 0, 0, 0, frandom(2,-2))
    Goto FireFinish
  FireS:
	TNT1 A 0 A_JumpIfInventory("RLSharpshooterPerk",1,5)
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziAmmochain",4,2)
	TNT1 A 0 A_JumpIfInventory("RLAmmochainPerk",1,2)
	TNT1 A 0 A_FireCustomMissile ("RLPlasmaUziBallS", 0, 1)
    Goto FireFinish
	TNT1 A 0 A_FireCustomMissile ("RLPlasmaUziBallS", 0, 0)
    Goto FireFinish
	TNT1 A 0 A_FireCustomMissile ("RLPlasmaUziBallSSharpshooter", 0, 1)
    Goto FireFinish
  FireFinish:
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziFirestormMod",1,"FireFinishF")
    PUZI E 1 Bright A_GunFlash
    PUZI F 1 Bright
    PUZI G 1
    Goto FireFinishContinue
  FireFinishF:
    PUZI E 1 Bright A_GunFlash
    PUZI F 1 Bright
    Goto FireFinishContinue
  FireFinishContinue:
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziAmmochain",4,2)
	TNT1 A 0 A_Jump(256,2)
	TNT1 A 0 A_TakeInventory("RLPlasmaUziAmmochain",4)
    TNT1 A 0 A_ReFire
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziFirestormMod",1,1)
    Goto Ready
    PUZI G 1
    Goto Ready
  Reload:
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziNanoMod",1,"Ready")
	PUZI BCD 1 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziBulkMod",1,2)
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziClip", 32, "Ready")
	Goto Reload+7
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziClip", 42, "Ready")
	TNT1 A 0 A_JumpIfInventory("Cell", 1, 1)
	Goto Ready
	TNT1 A 0 // Play the reload animation.
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziTechnicalMod",1,"Reload1")
    PUZI H 2
    PUZI A 3
    PUZI I 2
    PUZI JKLM 1
    TNT1 A 0 A_PlaySound("weapons/plasmauziunload", 5)
    TNT1 A 34
	Goto ReloadWorking
  Reload1:
    PUZI H 2
    PUZI A 3
    PUZI I 2
    PUZI JKLM 1
    TNT1 A 0 A_PlaySound("weapons/plasmauziunload", 5)
    TNT1 A 31
	Goto ReloadWorking
  ReloadWorking:
	TNT1 A 0 A_TakeInventory("Cell", 1)
	TNT1 A 0 A_GiveInventory("RLPlasmaUziClip", 1)
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziBulkMod",1,2)
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziClip",32,3)
	Goto ReloadWorking+5
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziClip",42,2)
	TNT1 A 0 A_JumpIfInventory("Cell",1,2)
	TNT1 A 0
	Goto ReloadFinish
	TNT1 A 0
	Goto ReloadWorking
  ReloadFinish:
    TNT1 A 0 A_PlaySound("weapons/plasmauziload", 7)
	TNT1 A 0 A_JumpIfInventory("RLPlasmaUziTechnicalMod",1,"ReloadFinish1")
    TNT1 A 4
    PUZI MLKJ 1
    PUZI I 2
    PUZI A 3
    PUZI H 2
	Goto Ready
  ReloadFinish1:
    TNT1 A 1
    PUZI MLKJ 1
    PUZI I 2
    PUZI A 3
    PUZI H 2
	Goto Ready
  Flash:
    TNT1 A 1 A_Light2
    TNT1 A 1 A_Light1
    Goto LightDone
  Spawn:
    PUZI X -1
    Stop
  Altfire:
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate",1, "ArmorModConversion")
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"ScavengerTime")
	Goto Ready
  ScavengerTime:
	TNT1 A 0 A_Print("Mod pack extracted from discarded weapon.")
    TNT1 A 0 A_StopSound(6)
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
    TNT1 A 0 A_TakeInventory("RLPlasmaUziModLimit", 1)
    TNT1 A 0 A_TakeInventory("RLPlasmaUziPowerMod", 1)
    TNT1 A 0 A_TakeInventory("RLPlasmaUziBulkMod", 1)
    TNT1 A 0 A_TakeInventory("RLPlasmaUziAgilityMod", 1)
    TNT1 A 0 A_TakeInventory("RLPlasmaUziTechnicalMod", 1)
    TNT1 A 0 A_TakeInventory("RLPlasmaUziSniperMod", 1)
    TNT1 A 0 A_TakeInventory("RLPlasmaUziFirestormMod", 1)
    TNT1 A 0 A_TakeInventory("RLPlasmaUziNanoMod", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLPlasmaUziSelected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
    TNT1 A 0 A_StopSound(6)
	TNT1 A 0 A_Jump(256,1,2,3)
	TNT1 A 0 A_FireCustomMissile("DroppedRLPowerMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLPowerMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLTechnicalMod",0,0,0)
	Goto ScavengerTimeEnd
  ScavengerTimeEnd:
	TNT1 A 1 A_TakeInventory("RLPlasmaUzi",1)
	Stop
  }
}

//-------------------------------------------------------------------
//		PROJECTILES

ACTOR RLPlasmaUziBall : PlasmaBall
{
  Radius 7
  Height 4
  Speed 45
  Scale 0.5
  Alpha 0.85
  Damage 8
  Species "Player"
  +THRUSPECIES
  +THRUGHOST
  Decal "PlasmaScorch"
  Damagetype "Plasma"
  SeeSound "weapons/plasmauzi"
  DeathSound "weapons/plasmauziimpact"
}

ACTOR RLPlasmaUziBallP : RLPlasmaUziBall
{
  Damage 10
}

ACTOR RLPlasmaUziBallA : RLPlasmaUziBall
{
  Speed 55
}

ACTOR RLPlasmaUziBallS : RLPlasmaUziBall
{
  States
  {
  Spawn:
    PLSS AB 6 Bright A_ScaleVelocity(1.10)
    Loop
  }
}

//-------------------------------------------------------------------
//		SHARPSHOOTER PROJECTILES

ACTOR RLPlasmaUziBallSharpshooter : RLPlasmaUziBall
{
  Damage (64)
}

ACTOR RLPlasmaUziBallPSharpshooter : RLPlasmaUziBallSharpshooter
{
  Damage (80)
}

ACTOR RLPlasmaUziBallASharpshooter : RLPlasmaUziBallSharpshooter
{
  Speed 55
}

ACTOR RLPlasmaUziBallSSharpshooter : RLPlasmaUziBallSharpshooter
{
  States
  {
  Spawn:
    PLSS AB 6 Bright A_ScaleVelocity(1.10)
    Loop
  }
}

//-------------------------------------------------------------------
//		MECHANICS

Actor RLPlasmaUziSelected : Inventory
{
inventory.maxamount 1
}

Actor RLPlasmaUziAmmochain : Inventory
{
inventory.maxamount 4
}

Actor RLPlasmaUziState : Inventory
{
inventory.maxamount 1
}

Actor RLPlasmaUziModLimit : Inventory
{
inventory.maxamount 1
}

Actor RLPlasmaUziPowerMod : Inventory
{
inventory.maxamount 1
}

Actor RLPlasmaUziBulkMod : Inventory
{
inventory.maxamount 1
}

Actor RLPlasmaUziAgilityMod : Inventory
{
inventory.maxamount 1
}

Actor RLPlasmaUziTechnicalMod : Inventory
{
inventory.maxamount 1
}

Actor RLPlasmaUziFirestormMod : Inventory
{
inventory.maxamount 1
}

Actor RLPlasmaUziNanoMod : Inventory
{
inventory.maxamount 1
}

Actor RLPlasmaUziSniperMod : Inventory
{
inventory.maxamount 1
}

ACTOR RLPlasmaUziClip : Ammo
{
   +INVENTORY.IGNORESKILL
   Inventory.MaxAmount 42
   Inventory.Icon ""
}

ACTOR RLPlasmaUziPickup : CustomInventory
{
	Radius 16
	Height 8
	Scale 0.5
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
	Spawn:
		PUZI X -1
		Stop
	Pickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("RLPlasmaUzi",1,"NotPickup") // If the player already has this weapon, they are not given the weapon
		TNT1 A 0 A_JumpIfInventory("RLWeaponLimit",0,"NotPickup2") // The absolute max limit check for weapons
		TNT1 A 0 A_PlaySound("weapons/plasmauzipickup")
		TNT1 A 0 A_GiveInventory("RLPlasmaUzi",1)
		TNT1 A 0 A_GiveInventory("RLWeaponLimit",1)
		TNT1 A 0 A_Print("Picked up a \cvPlasma Uzi.")
		Stop
	NotPickup:
		TNT1 A 0 A_JumpIfInventory("Cell",0,1)
		Goto AmmoPickup
	NotPickup2:
		TNT1 A 0 A_JumpIfInventory("DRLA_WeaponAntiLogFloodCheck",1,"NotPickup_NoPrint")
		TNT1 A 0 A_GiveInventory("DRLA_WeaponAntiLogFloodCheck")
		TNT1 A 0 A_Print("\cv= Plasma Uzi =", 2, "CONFONT")
	 NotPickup_NoPrint:	
		TNT1 A 0
		Fail
	AmmoPickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("Cell",0,"AmmoFull")
		TNT1 A 0 A_PlaySound("misc/w_pkup")
		TNT1 A 1 A_GiveInventory("Cell",32)
		Stop
	AmmoFull:
		PUZI X 1
		Fail
	}
}

ACTOR DroppedRLPlasmaUzi
{
	PROJECTILE
	-NOGRAVITY
	Damage 0
	Radius 16
	Height 8
	Speed 9
	Scale 0.5
	DeathSound "weapons/gundrop"
	States
	{
	Spawn:
		PUZI X 1
		Loop
	Death:
		TNT1 A 0
		PUZI X 1 A_SpawnItem("RLPlasmaUziPickup",0,0,0,0)
		Stop
	}
}
