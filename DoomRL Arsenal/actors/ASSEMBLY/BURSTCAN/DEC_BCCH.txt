
ACTOR RLBurstCannonChaingun : RLWeapon
{
  Inventory.Pickupsound "misc/w_pkup"
  Weapon.UpSound "weapons/weaponholster"
  Obituary "%o was perforated by %k's chaingun."
  Inventory.Pickupmessage "You picked up a Burst Chaingun."
  Decal BulletChip
  +AMMO_OPTIONAL
  +NOALERT
  +INVENTORY.UNTOSSABLE
  Tag "Burst Chaingun"
  Weapon.Kickback 40
  Weapon.SelectionOrder 25
  Weapon.SlotNumber 4
  Weapon.AmmoUse1 1
  Weapon.AmmoGive1 0
  Weapon.AmmoType1 "RLBurstCannonChaingunClip"
  Weapon.AmmoType2 "Clip"
  States
  {
  Ready:
	TNT1 A 0 A_JumpIfInventory("RLRenegadeAssemblyCheck",1,"RenegadeAssemblyNoFire")
	TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunBurst",3)
	TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunState",1)
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunNanoMod",1,2)
    PKCG A 1 A_WeaponReady (48)
    Loop
  NanoRegenCheck:
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunClip",1,2)
    PKCG A 70 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
	Goto NanoRegenCheckBegin
  NanoRegenCheckBegin:
    PKCG A 70 A_WeaponReady (32)
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunBulkMod",1,2)
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunClip", 80, "Ready")
	Goto NanoRegen
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunClip", 104, "Ready")
	Goto NanoRegen
  NanoRegen:
	TNT1 A 0 A_GiveInventory("RLBurstCannonChaingunClip", 1)
    PKCG A 17 A_WeaponReady (32)
    Goto NanoRegenCheckBegin+1
  ReadyAfterFire:
    TNT1 A 0 A_PlaySound("weapons/burstchaingunwinddown", 6)
	PKCG AB 3 A_WeaponReady (WRF_NOFIRE | WRF_ALLOWRELOAD | WRF_ALLOWZOOM | WRF_NOBOB)
	PKCG CD 4 A_WeaponReady (WRF_NOFIRE | WRF_ALLOWRELOAD | WRF_ALLOWZOOM | WRF_NOBOB)
    PKCG A 5 A_WeaponReady (WRF_ALLOWRELOAD | WRF_ALLOWZOOM | WRF_NOBOB)
	PKCG B 5
	PKCG CD 6
    Goto Ready
  ScavengerDrop:
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"WeaponDropConfirm")
	TNT1 A 0 A_GiveInventory("RLScavengerDrop",1)
	TNT1 A 0 A_Print("Press again to drop current weapon, or press Alt Fire to scrap it for a Mod Pack.")
	Goto AfterModDrop
  WeaponDropConfirm:
	TNT1 A 0 A_JumpIfInventory("RLShellshockPerk",1, 1)
	Goto WeaponDropConfirm+2
	TNT1 A 0 A_GiveInventory("RLRenegadeAssemblyCheck",1)
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
    TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunModLimit", 1)
    TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunPowerMod", 1)
    TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunBulkMod", 1)
    TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunAgilityMod", 1)
    TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunTechnicalMod", 1)
    TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunFirestormMod", 1)
    TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunSniperMod", 1)
    TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunNanoMod", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunSelected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
	TNT1 A 0 A_FireCustomMissile("DroppedRLBurstCannonChaingun",0,0,0)
	TNT1 A 1 A_TakeInventory("RLBurstCannonChaingun",1)
	Stop
  AfterModDrop:
    PKCG A 8 A_WeaponReady (16)
    Goto Ready
  RenegadeAssemblyNoFire:
	TNT1 A 0 A_TakeInventory("RLRenegadeAssemblyCheck",1)
    PKCG A 9 A_WeaponReady (WRF_NOFIRE | WRF_NOBOB)
    Goto Ready
  Deselect:
	TNT1 A 0
	TNT1 A 0 A_TakeInventory("RLArmorModItemActivate",1)
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
	TNT1 A 0 A_TakeInventory("RLUseBulkMod",1)
	TNT1 A 0 A_TakeInventory("RLUsePowerMod",1)
	TNT1 A 0 A_TakeInventory("RLUseAgilityMod",1)
	TNT1 A 0 A_TakeInventory("RLUseTechnicalMod",1)
	TNT1 A 0 A_TakeInventory("RLUseNanoMod",1)
	TNT1 A 0 A_TakeInventory("RLUseFirestormMod",1)
	TNT1 A 0 A_TakeInventory("RLUseSniperMod",1)
	TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunSelected", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
  DeselectLoop:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "DeselectRenegade")
    PKCG A 1 A_Lower
    Loop
  DeselectRenegade:
    TNT1 A 0 A_Lower
    loop
  SelectRenegade: 
    TNT1 A 0 A_Raise
    loop
  SelectSetWeapon:
	TNT1 A 0 A_GiveInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_GiveInventory("RLBurstCannonChaingunSelected", 1)
    Goto SelectLoop
  SelectLoop:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "SelectRenegade")
    PKCG A 1 A_Raise
    Loop
  Fire:
	TNT1 A 0 A_JumpIfNoAmmo("Reload")
	TNT1 A 0 A_AlertMonsters
	TNT1 A 0 A_GiveInventory("RLBurstCannonChaingunBurst",1)
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunSniperMod",1,"FireS")
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunPowerMod",1,"FireP")
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunAgilityMod",1,"FireA")
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunBurst",3,2)
	TNT1 A 0 A_JumpIfInventory("RLAmmochainPerk",1,2)
	TNT1 A 0 A_FireBullets (7, 3, -1, 10, "RLBulletPuff")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (7, 3, -1, 10, "RLBulletPuff", "None")
    Goto FireFinish
  FireP:
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunBurst",3,2)
	TNT1 A 0 A_JumpIfInventory("RLAmmochainPerk",1,2)
	TNT1 A 0 A_FireBullets (7, 3, -1, 12, "RLBulletPuff")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (7, 3, -1, 12, "RLBulletPuff", "None")
    Goto FireFinish
  FireA:
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunBurst",3,2)
	TNT1 A 0 A_JumpIfInventory("RLAmmochainPerk",1,2)
	TNT1 A 0 A_FireBullets (6.5, 2.5, -1, 10, "RLBulletPuff")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (6.5, 2.5, -1, 10, "RLBulletPuff", "None")
    Goto FireFinish
  FireS:
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunBurst",3,2)
	TNT1 A 0 A_JumpIfInventory("RLAmmochainPerk",1,2)
	TNT1 A 0 A_FireBullets (5, 1, -1, 10, "RLBulletPuff")
    Goto FireFinish
	TNT1 A 0 A_FireBullets (5, 1, -1, 10, "RLBulletPuff", "None")
    Goto FireFinish
  FireFinish:
    TNT1 A 0 A_PlaySound("weapons/burstchaingun", CHAN_WEAPON)
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunFirestormMod",1,1)
    Goto FireFinish+4
    PKCG A 1 A_GunFlash
    PKCG C 1
    Goto FireFinish+7
    PKCG A 1 A_GunFlash
    PKCG BC 1
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunState",1,2)
	TNT1 A 0 A_GiveInventory("RLBurstCannonChaingunState",1)
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunBurst",3,"FireEndBurst")
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunBurst",2,"FireEnd")
    Goto Fire
  FireEnd:
	TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunState",1)
    Goto Fire
  FireEndBurst:
	TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunBurst",3)
    Goto ReadyAfterFire
  Reload:
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunNanoMod",1,"Ready")
	PKCG A 1 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunBulkMod",1,2)
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunClip", 80, "Ready")
	Goto Reload+5
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunClip", 104, "Ready")
	TNT1 A 0 A_JumpIfInventory("Clip", 1, 1)
	Goto Ready
	TNT1 A 0 // Play the reload animation.
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunTechnicalMod",1,"Reload1")
    CHGG C 5
    CHGG D 5
    CHGG E 5
    CHGG F 5 A_PlaySound("weapons/burstchaingununload", 5)
    CHGG F 5
	Goto ReloadWorking
  Reload1:
    CHGG C 4
    CHGG D 4
    CHGG E 4
    CHGG F 5 A_PlaySound("weapons/burstchaingununload", 5)
    CHGG F 5
  ReloadWorking:
	TNT1 A 0 A_TakeInventory("Clip", 1)
	TNT1 A 0 A_GiveInventory("RLBurstCannonChaingunClip", 1)
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunBulkMod",1,2)
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunClip",80,3)
	Goto ReloadWorking+5
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunClip",104,2)
	TNT1 A 0 A_JumpIfInventory("Clip",1,2)
	TNT1 A 0
	Goto ReloadFinish
	TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunBurst", 3)
	Goto ReloadWorking
  ReloadFinish:
    TNT1 A 0 A_PlaySound("weapons/burstchaingunload", 7)
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunTechnicalMod",1,"ReloadFinish1")
    CHGG G 4
    CHGG F 4
    CHGG E 4
    CHGG D 4
    CHGG C 4
	Goto ReloadSpin
  ReloadFinish1:
    CHGG G 3
    CHGG F 3
    CHGG E 3
    CHGG D 4
    CHGG C 4
	Goto ReloadSpin
  ReloadSpin:
    TNT1 A 0 A_PlaySound("weapons/burstchaingunwinddown", 6)
	PKCG AB 3
	PKCG CD 4
	TNT1 A 0 A_JumpIfNoAmmo(2)
	TNT1 A 0 A_WeaponReady(WRF_NOBOB | WRF_NOSWITCH)
	PKCG AB 5
	PKCG CD 6
	Goto Ready
  Flash:
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunFirestormMod",1,"FlashFirestorm")
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunState",1,4)
    PKCF A 1 Bright A_Light2
    PKCF B 1 Bright A_Light1
	TNT1 A 0
    Goto LightDone
    PKCF C 1 Bright A_Light2
    PKCF D 1 Bright A_Light1
	TNT1 A 0
    Goto LightDone
  FlashFirestorm:
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingunState",1,3)
    PKCF A 1 Bright A_Light2
	TNT1 A 0
    Goto LightDone
    PKCF C 1 Bright A_Light1
	TNT1 A 0
    Goto LightDone
  Spawn:
    MGUN A -1
    Stop
  Altfire:
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate",1, "ArmorModConversion")
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"ScavengerTime")
	Goto Ready
  ScavengerTime:
	TNT1 A 0 A_Print("Mod pack extracted from discarded weapon.")
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
    TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunModLimit", 1)
    TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunPowerMod", 1)
    TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunBulkMod", 1)
    TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunAgilityMod", 1)
    TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunTechnicalMod", 1)
    TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunSniperMod", 1)
    TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunFirestormMod", 1)
    TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunNanoMod", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLBurstCannonChaingunSelected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
	TNT1 A 0 A_Jump(256,1,2,3)
	TNT1 A 0 A_FireCustomMissile("DroppedRLBulkMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLBulkMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLPowerMod",0,0,0)
	Goto ScavengerTimeEnd
  ScavengerTimeEnd:
	TNT1 A 1 A_TakeInventory("RLBurstCannonChaingun",1)
	Stop
  }
}

//-------------------------------------------------------------------
//		MECHANICS

Actor RLBurstCannonChaingunSelected : Inventory
{
inventory.maxamount 1
}

Actor RLBurstCannonChaingunState : Inventory
{
inventory.maxamount 1
}

Actor RLBurstCannonChaingunBurst : Inventory
{
inventory.maxamount 3
}

Actor RLBurstCannonChaingunModLimit : Inventory
{
inventory.maxamount 1
}

Actor RLBurstCannonChaingunPowerMod : Inventory
{
inventory.maxamount 1
}

Actor RLBurstCannonChaingunBulkMod : Inventory
{
inventory.maxamount 1
}

Actor RLBurstCannonChaingunAgilityMod : Inventory
{
inventory.maxamount 1
}

Actor RLBurstCannonChaingunTechnicalMod : Inventory
{
inventory.maxamount 1
}

Actor RLBurstCannonChaingunFirestormMod : Inventory
{
inventory.maxamount 1
}

Actor RLBurstCannonChaingunNanoMod : Inventory
{
inventory.maxamount 1
}

Actor RLBurstCannonChaingunSniperMod : Inventory
{
inventory.maxamount 1
}

ACTOR RLBurstCannonChaingunClip : Ammo
{
   +INVENTORY.IGNORESKILL
   Inventory.MaxAmount 104
   Inventory.Icon ""
}

ACTOR RLBurstCannonChaingunPickup : CustomInventory
{
	Radius 16
	Height 8
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
	Spawn:
		MGUN A -1
		Stop
	Pickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("RLBurstCannonChaingun",1,"NotPickup") // If the player already has this weapon, they are not given the weapon
		TNT1 A 0 A_JumpIfInventory("RLWeaponLimit",0,"NotPickup2") // The absolute max limit check for weapons
		TNT1 A 0 A_PlaySound("weapons/burstchaingunpickup")
		TNT1 A 0 A_GiveInventory("RLBurstCannonChaingun",1)
		TNT1 A 0 A_GiveInventory("RLWeaponLimit",1)
		TNT1 A 0 A_Print("Picked up a \cvBurst Chaingun.")
		Stop
	NotPickup:
		TNT1 A 0 A_JumpIfInventory("Clip",0,1)
		Goto AmmoPickup
	NotPickup2:
		TNT1 A 0 A_JumpIfInventory("DRLA_WeaponAntiLogFloodCheck",1,"NotPickup_NoPrint")
		TNT1 A 0 A_GiveInventory("DRLA_WeaponAntiLogFloodCheck")
		TNT1 A 0 A_Print("\cv= Burst Chaingun =", 2, "CONFONT")
	 NotPickup_NoPrint:	
		TNT1 A 0
		Fail
	AmmoPickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("Clip",0,"AmmoFull")
		TNT1 A 0 A_PlaySound("misc/w_pkup")
		TNT1 A 1 A_GiveInventory("Clip",80)
		Stop
	AmmoFull:
		MGUN A 1
		Fail
	}
}

ACTOR DroppedRLBurstCannonChaingun
{
	PROJECTILE
	-NOGRAVITY
	Damage 0
	Radius 16
	Height 8
	Speed 9
	DeathSound "weapons/gundrop"
	States
	{
	Spawn:
		MGUN A 1
		Loop
	Death:
		TNT1 A 0
		MGUN A 1 A_SpawnItem("RLBurstCannonChaingunPickup",0,0,0,0)
		Stop
	}
}
