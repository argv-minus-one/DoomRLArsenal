
ACTOR RLAssaultRiflePlasmaRifle : RLWeapon
{
  Inventory.Pickupsound "misc/w_pkup"
  Weapon.UpSound "weapons/weaponholster"
  Obituary "%o was burnt to ashes by %k's plasma rifle."
  Inventory.Pickupmessage "You picked up an Assault Plasma Rifle."
  +AMMO_OPTIONAL
  +NOALERT
  +INVENTORY.UNTOSSABLE
  Tag "Assault Plasma Rifle"
  Weapon.Kickback 40
  Weapon.SelectionOrder 20
  Weapon.SlotNumber 6
  Weapon.AmmoUse1 1
  Weapon.AmmoGive1 0
  Weapon.AmmoType1 "RLAssaultRiflePlasmaRifleClip"
  Weapon.AmmoType2 "Cell"
  States
  {
  Ready:
	TNT1 A 0 A_JumpIfInventory("RLRenegadeAssemblyCheck",1,"RenegadeAssemblyNoFire")
	TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleState",1)
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleNanoMod",1,2)
    PLSG A 1 A_WeaponReady (48)
    Loop
  NanoRegenCheck:
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleClip",1,2)
    PLSG A 70 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
	Goto NanoRegenCheckBegin
  NanoRegenCheckBegin:
    PLSG A 70 A_WeaponReady (32)
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleBulkMod",1,2)
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleClip", 40, "Ready")
	Goto NanoRegen
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleClip", 52, "Ready")
	Goto NanoRegen
  NanoRegen:
	TNT1 A 0 A_GiveInventory("RLAssaultRiflePlasmaRifleClip", 1)
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleNanoMod",2,2)
    PLSG A 17 A_WeaponReady (32)
    Goto NanoRegenCheckBegin+1
    PLSG A 9 A_WeaponReady (32)
    Goto NanoRegenCheckBegin+1
  ReadyAfterFire:
    PLSG A 14 A_WeaponReady (WRF_NOFIRE | WRF_ALLOWRELOAD | WRF_ALLOWZOOM | WRF_NOBOB)
    PLSG A 10 A_WeaponReady (WRF_ALLOWRELOAD | WRF_ALLOWZOOM | WRF_NOBOB)
    Goto Ready
  ScavengerDrop:
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"WeaponDropConfirm")
	TNT1 A 0 A_GiveInventory("RLScavengerDrop",1)
	TNT1 A 0 A_Print("Press again to drop current weapon, or press Alt Fire to scrap it for a Mod Pack.")
	Goto AfterModDrop
  WeaponDropConfirm:
	TNT1 A 0 A_JumpIfInventory("RLShellshockPerk",1, 1)
	Goto WeaponDropConfirm+2
	TNT1 A 0 A_GiveInventory("RLRenegadeAssemblyCheck",1)
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
    TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleModLimit", 1)
    TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRiflePowerMod", 1)
    TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleBulkMod", 1)
    TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleAgilityMod", 1)
    TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleTechnicalMod", 1)
    TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleFirestormMod", 1)
    TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleSniperMod", 1)
    TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleNanoMod", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleSelected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
	TNT1 A 0 A_FireCustomMissile("DroppedRLAssaultRiflePlasmaRifle",0,0,0)
	TNT1 A 1 A_TakeInventory("RLAssaultRiflePlasmaRifle",1)
	Stop
  AfterModDrop:
    PLSG A 8 A_WeaponReady (16)
    Goto Ready
  RenegadeAssemblyNoFire:
	TNT1 A 0 A_TakeInventory("RLRenegadeAssemblyCheck",1)
    PLSG A 9 A_WeaponReady (WRF_NOFIRE | WRF_NOBOB)
    Goto Ready
  Deselect:
	TNT1 A 0
	TNT1 A 0 A_TakeInventory("RLArmorModItemActivate",1)
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
	TNT1 A 0 A_TakeInventory("RLUseBulkMod",1)
	TNT1 A 0 A_TakeInventory("RLUsePowerMod",1)
	TNT1 A 0 A_TakeInventory("RLUseAgilityMod",1)
	TNT1 A 0 A_TakeInventory("RLUseTechnicalMod",1)
	TNT1 A 0 A_TakeInventory("RLUseNanoMod",1)
	TNT1 A 0 A_TakeInventory("RLUseFirestormMod",1)
	TNT1 A 0 A_TakeInventory("RLUseSniperMod",1)
	TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleSelected", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
  DeselectLoop:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "DeselectRenegade")
    PLSG A 1 A_Lower
    Loop
  DeselectRenegade:
    TNT1 A 0 A_Lower
    loop
  SelectRenegade: 
    TNT1 A 0 A_Raise
    loop
  SelectSetWeapon:
	TNT1 A 0 A_GiveInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_GiveInventory("RLAssaultRiflePlasmaRifleSelected", 1)
    Goto SelectLoop
  SelectLoop:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "SelectRenegade")
    PLSG A 1 A_Raise
    Loop
  Fire:
	TNT1 A 0 A_JumpIfNoAmmo("Reload")
	TNT1 A 0 A_AlertMonsters
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleSniperMod",1,"FireS")
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRiflePowerMod",1,"FireP")
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleAgilityMod",1,"FireA")
	TNT1 A 0 A_JumpIfInventory("RLXaserPowerArmorToken",1,"XaserFire")
	TNT1 A 0 A_FireCustomMissile ("RLAssaultPlasmaBall", 0, 1)
    Goto FireFinish
  XaserFire:
	TNT1 A 0 A_FireCustomMissile ("RLXaserAssaultPlasmaBall", 0, 1)
	TNT1 A 0 A_FireCustomMissile ("RLXaserSAssaultPlasmaBall", -30, 0)
	TNT1 A 0 A_FireCustomMissile ("RLXaserSAssaultPlasmaBall", 30, 0)
    Goto FireFinish
  FireP:
	TNT1 A 0 A_JumpIfInventory("RLXaserPowerArmorToken",1,"XaserFireP")
	TNT1 A 0 A_FireCustomMissile ("RLAssaultPlasmaBallP", 0, 1)
    Goto FireFinish
  XaserFireP:
	TNT1 A 0 A_FireCustomMissile ("RLXaserAssaultPlasmaBallP", 0, 1)
	TNT1 A 0 A_FireCustomMissile ("RLXaserSAssaultPlasmaBallP", -30, 0)
	TNT1 A 0 A_FireCustomMissile ("RLXaserSAssaultPlasmaBallP", 30, 0)
    Goto FireFinish
  FireA:
	TNT1 A 0 A_JumpIfInventory("RLXaserPowerArmorToken",1,"XaserFireA")
	TNT1 A 0 A_FireCustomMissile ("RLAssaultPlasmaBallA", 0, 1)
    Goto FireFinish
  XaserFireA:
	TNT1 A 0 A_FireCustomMissile ("RLXaserAssaultPlasmaBallA", 0, 1)
	TNT1 A 0 A_FireCustomMissile ("RLXaserSAssaultPlasmaBallA", -30, 0)
	TNT1 A 0 A_FireCustomMissile ("RLXaserSAssaultPlasmaBallA", 30, 0)
    Goto FireFinish
  FireS:
	TNT1 A 0 A_JumpIfInventory("RLXaserPowerArmorToken",1,"XaserFireS")
	TNT1 A 0 A_FireCustomMissile ("RLAssaultPlasmaBallS", 0, 1)
    Goto FireFinish
  XaserFireS:
	TNT1 A 0 A_FireCustomMissile ("RLXaserAssaultPlasmaBallS", 0, 1)
	TNT1 A 0 A_FireCustomMissile ("RLXaserSAssaultPlasmaBallS", -30, 0)
	TNT1 A 0 A_FireCustomMissile ("RLXaserSAssaultPlasmaBallS", 30, 0)
    Goto FireFinish
  FireFinish:
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleState",1,"FireState")
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleFirestormMod",1,1)
    Goto FireFinish+3
    PLSF A 2 Bright A_GunFlash
    Goto FireFinish+4
    PLSF A 3 Bright A_GunFlash
	TNT1 A 0 A_GiveInventory("RLAssaultRiflePlasmaRifleState",1)
    Goto ReadyAfterFire
  FireState:
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleFirestormMod",1,1)
    Goto FireState+2
    PLSF B 2 Bright A_GunFlash
    Goto FireState+3
    PLSF B 3 Bright A_GunFlash
	TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleState",1)
    Goto ReadyAfterFire
  Reload:
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleNanoMod",1,"Ready")
	PLSG A 1 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleBulkMod",1,2)
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleClip", 40, "Ready")
	Goto Reload+5
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleClip", 52, "Ready")
	TNT1 A 0 A_JumpIfInventory("Cell", 1, 1)
	Goto Ready
	TNT1 A 0 // Play the reload animation.
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleTechnicalMod",1,"Reload1")
    PLSG C 4
    PLSG D 4
    PLSG E 4
    PLSG F 4
    PLSG G 4
    PLSG H 4
    PLSG I 4 A_PlaySound("weapons/assaultplasmarifleunload", 5)
    PLSG I 4
	Goto ReloadWorking
  Reload1:
    PLSG C 3
    PLSG D 3
    PLSG E 3
    PLSG F 4
    PLSG G 4
    PLSG H 4
    PLSG I 4 A_PlaySound("weapons/assaultplasmarifleunload", 5)
    PLSG I 4
  ReloadWorking:
	TNT1 A 0 A_TakeInventory("Cell", 1)
	TNT1 A 0 A_GiveInventory("RLAssaultRiflePlasmaRifleClip", 1)
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleBulkMod",1,2)
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleClip",40,3)
	Goto ReloadWorking+5
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleClip",52,2)
	TNT1 A 0 A_JumpIfInventory("Cell",1,2)
	TNT1 A 0
	Goto ReloadFinish
	TNT1 A 0
	Goto ReloadWorking
  ReloadFinish:
    PLSG I 2
    TNT1 A 0 A_PlaySound("weapons/assaultplasmarifleload", 6)
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleTechnicalMod",1,"ReloadFinish1")
    PLSG H 4
	PLSG G 4
	PLSG F 4
	PLSG E 4
	PLSG D 4
	PLSG C 4
	Goto Ready
  ReloadFinish1:
    PLSG H 3
	PLSG G 3
	PLSG F 3
	PLSG E 4
	PLSG D 4
	PLSG C 4
	Goto Ready
  Flash:
	TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifleFirestormMod",1,1)
    Goto Flash+2
    TNT1 A 2 Bright A_Light2
    Goto LightDone
    TNT1 A 3 Bright A_Light2
    Goto LightDone
  Spawn:
    PLAS A -1
    Stop
  Altfire:
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate",1, "ArmorModConversion")
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"ScavengerTime")
	Goto Ready
  ScavengerTime:
	TNT1 A 0 A_Print("Mod pack extracted from discarded weapon.")
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
    TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleModLimit", 1)
    TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRiflePowerMod", 1)
    TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleBulkMod", 1)
    TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleAgilityMod", 1)
    TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleTechnicalMod", 1)
    TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleSniperMod", 1)
    TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleFirestormMod", 1)
    TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleNanoMod", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssaultRiflePlasmaRifleSelected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
	TNT1 A 0 A_FireCustomMissile("DroppedRLAgilityMod",0,0,0)
	Goto ScavengerTimeEnd
  ScavengerTimeEnd:
	TNT1 A 1 A_TakeInventory("RLAssaultRiflePlasmaRifle",1)
	Stop
  }
}

//-------------------------------------------------------------------
//		PROJECTILES

ACTOR RLAssaultPlasmaBall : RLPlasmaBall
{
  Speed 50
  Damage 15
}

ACTOR RLAssaultPlasmaBallP : RLPlasmaBall
{
  Damage 20
}

ACTOR RLAssaultPlasmaBallA : RLPlasmaBall
{
  Speed 60
}

ACTOR RLAssaultPlasmaBallS : RLPlasmaBall
{
  States
  {
  Spawn:
    PLSS AB 4 Bright A_ScaleVelocity(1.10)
    Loop
  }
}

ACTOR RLXaserAssaultPlasmaBall : RLPlasmaBall
{
  Speed 50
  Damage 15
  SeeSound "weapons/xaserplasma"
}

ACTOR RLXaserAssaultPlasmaBallP : RLXaserAssaultPlasmaBall
{
  Damage 20
}

ACTOR RLXaserAssaultPlasmaBallA : RLXaserAssaultPlasmaBall
{
  Speed 60
}

ACTOR RLXaserAssaultPlasmaBallS : RLXaserAssaultPlasmaBall
{
  States
  {
  Spawn:
    PLSS AB 4 Bright A_ScaleVelocity(1.10)
    Loop
  }
}

ACTOR RLXaserSAssaultPlasmaBall : RLPlasmaBall
{
  Speed 50
  Damage 15
  SeeSound ""
}

ACTOR RLXaserSAssaultPlasmaBallP : RLXaserSAssaultPlasmaBall
{
  Damage 20
}

ACTOR RLXaserSAssaultPlasmaBallA : RLXaserSAssaultPlasmaBall
{
  Speed 60
}

ACTOR RLXaserSAssaultPlasmaBallS : RLXaserSAssaultPlasmaBall
{
  States
  {
  Spawn:
    PLSS AB 4 Bright A_ScaleVelocity(1.10)
    Loop
  }
}

//-------------------------------------------------------------------
//		MECHANICS

Actor RLAssaultRiflePlasmaRifleSelected : Inventory
{
inventory.maxamount 1
}

Actor RLAssaultRiflePlasmaRifleState : Inventory
{
inventory.maxamount 1
}

Actor RLAssaultRiflePlasmaRifleModLimit : Inventory
{
inventory.maxamount 1
}

Actor RLAssaultRiflePlasmaRiflePowerMod : Inventory
{
inventory.maxamount 1
}

Actor RLAssaultRiflePlasmaRifleBulkMod : Inventory
{
inventory.maxamount 1
}

Actor RLAssaultRiflePlasmaRifleAgilityMod : Inventory
{
inventory.maxamount 1
}

Actor RLAssaultRiflePlasmaRifleTechnicalMod : Inventory
{
inventory.maxamount 1
}

Actor RLAssaultRiflePlasmaRifleFirestormMod : Inventory
{
inventory.maxamount 1
}

Actor RLAssaultRiflePlasmaRifleNanoMod : Inventory
{
inventory.maxamount 1
}

Actor RLAssaultRiflePlasmaRifleSniperMod : Inventory
{
inventory.maxamount 1
}

ACTOR RLAssaultRiflePlasmaRifleClip : Ammo
{
   +INVENTORY.IGNORESKILL
   Inventory.MaxAmount 52
   Inventory.Icon ""
}

ACTOR RLAssaultRiflePlasmaRiflePickup : CustomInventory
{
	Radius 16
	Height 8
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
	Spawn:
		PLAS A -1
		Stop
	Pickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("RLAssaultRiflePlasmaRifle",1,"NotPickup") // If the player already has this weapon, they are not given the weapon
		TNT1 A 0 A_JumpIfInventory("RLWeaponLimit",0,"NotPickup2") // The absolute max limit check for weapons
		TNT1 A 0 A_PlaySound("weapons/assaultplasmariflepickup")
		TNT1 A 0 A_GiveInventory("RLAssaultRiflePlasmaRifle",1)
		TNT1 A 0 A_GiveInventory("RLWeaponLimit",1)
		TNT1 A 0 A_Print("Picked up a \cvAssault Plasma Rifle.")
		Stop
	NotPickup:
		TNT1 A 0 A_JumpIfInventory("Cell",0,1)
		Goto AmmoPickup
	NotPickup2:
		TNT1 A 0 A_JumpIfInventory("DRLA_WeaponAntiLogFloodCheck",1,"NotPickup_NoPrint")
		TNT1 A 0 A_GiveInventory("DRLA_WeaponAntiLogFloodCheck")
		TNT1 A 0 A_Print("\cv= Assault Plasma Rifle =", 2, "CONFONT")
	 NotPickup_NoPrint:	
		TNT1 A 0
		Fail
	AmmoPickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("Cell",0,"AmmoFull")
		TNT1 A 0 A_PlaySound("misc/w_pkup")
		TNT1 A 1 A_GiveInventory("Cell",40)
		Stop
	AmmoFull:
		PLAS A 1
		Fail
	}
}

ACTOR DroppedRLAssaultRiflePlasmaRifle
{
	PROJECTILE
	-NOGRAVITY
	Damage 0
	Radius 16
	Height 8
	Speed 9
	DeathSound "weapons/gundrop"
	States
	{
	Spawn:
		PLAS A 1
		Loop
	Death:
		TNT1 A 0
		PLAS A 1 A_SpawnItem("RLAssaultRiflePlasmaRiflePickup",0,0,0,0)
		Stop
	}
}
