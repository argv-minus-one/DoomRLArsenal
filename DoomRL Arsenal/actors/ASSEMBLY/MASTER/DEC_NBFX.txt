
ACTOR RLNuclearBiggestFuckingGun : RLWeapon
{
  Inventory.Pickupsound "misc/w_pkup"
  Weapon.UpSound "weapons/weaponholster"
  Obituary "%o was completely overkilled by %k's Biggest Fucking Gun."
  Inventory.Pickupmessage "You picked up the Biggest Fucking Nuclear Gun. Time to take some names."
  +AMMO_OPTIONAL
  +NOALERT
  +INVENTORY.UNTOSSABLE
  Tag "Biggest Fucking Nuclear Gun"
  Weapon.Kickback 40
  Weapon.SelectionOrder 3900
  Weapon.SlotNumber 7
  Weapon.AmmoUse1 100
  Weapon.AmmoGive1 0
  Weapon.AmmoType1 "RLNuclearBiggestFuckingGunClip"
  States
  {
  Ready:
	TNT1 A 0 A_JumpIfInventory("RLRenegadeAssemblyCheck",1,"RenegadeAssemblyNoFire")
    TNT1 A 0 A_PlaySound("weapons/nukebfgxidle", 6, 1, 1)
	TNT1 A 0 A_JumpIfInventory("RLNuclearBiggestFuckingGunClip", 100, "ReadyCantAltFire")
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop", 1, "ReadyCanAltFire")
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate", 1, "ReadyCanAltFire")
	TNT1 A 0 A_TakeInventory("RLNuclearBiggestFuckingGunCharged",1)
    PRCG ABCABC 1 A_WeaponReady (WRF_NOFIRE | WRF_ALLOWZOOM)
	Goto ReadyCycleCharging
  ReadyCanAltFire:
	TNT1 A 0 A_TakeInventory("RLNuclearBiggestFuckingGunCharged",1)
    PRCG ABCABC 1 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
	Goto ReadyCycleCharging
  ReadyCantAltFire:
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop", 1, "ReadyCanFire")
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate", 1, "ReadyCanFire")
	TNT1 A 0 A_TakeInventory("RLNuclearBiggestFuckingGunCharged",1)
    PRCG ABCABC 1 A_WeaponReady (WRF_NOSECONDARY | WRF_ALLOWZOOM)
	Goto ReadyCycleCharged
  ReadyCanFire:
    PRCG ABCABC 1 A_WeaponReady (WRF_ALLOWZOOM)
	Goto ReadyCycleCharged
//=================================================================================
  ReadyCycleCharging:
	TNT1 A 0 A_JumpIfInventory("RLNuclearBiggestFuckingGunClip", 100, "ReadyBeep")
	Goto ReadyChargingAmmo
  ReadyChargingAmmo:
	TNT1 A 0 A_GiveInventory("RLNuclearBiggestFuckingGunClip", 1)
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop", 1, "ReadyChargingAmmo2")
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate", 1, "ReadyChargingAmmo2")
	TNT1 A 0 A_JumpIfInventory("RLNuclearWeaponSetBonusActive", 1, 7)
    PRCG ABCABC 1 A_WeaponReady (WRF_NOFIRE | WRF_ALLOWZOOM)
    Goto ReadyCycleCharging
    PRCG ABC 1 A_WeaponReady (WRF_NOFIRE | WRF_ALLOWZOOM)
    Goto ReadyCycleCharging
  ReadyChargingAmmo2:
	TNT1 A 0 A_JumpIfInventory("RLNuclearWeaponSetBonusActive", 1, 7)
    PRCG ABCABC 1 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
    Goto ReadyCycleCharging
    PRCG ABC 1 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
    Goto ReadyCycleCharging
//=================================================================================
  ReadyCycleCharged:
	TNT1 A 0 A_JumpIfInventory("RLNuclearBiggestFuckingGunClip", 100, "Ready")
	Goto ReadyChargedAmmo
  ReadyChargedAmmo:
	TNT1 A 0 A_GiveInventory("RLNuclearBiggestFuckingGunClip", 1)
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop", 1, "ReadyChargedAmmo2")
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate", 1, "ReadyChargedAmmo2")
	TNT1 A 0 A_JumpIfInventory("RLNuclearWeaponSetBonusActive", 1, 7)
    PRCG ABCABC 1 A_WeaponReady (WRF_NOSECONDARY | WRF_ALLOWZOOM)
    Goto ReadyCycleCharged
    PRCG ABC 1 A_WeaponReady (WRF_NOSECONDARY | WRF_ALLOWZOOM)
    Goto ReadyCycleCharged
  ReadyChargedAmmo2:
	TNT1 A 0 A_JumpIfInventory("RLNuclearWeaponSetBonusActive", 1, 7)
    PRCG ABCABC 1 A_WeaponReady (WRF_ALLOWZOOM)
    Goto ReadyCycleCharged
    PRCG ABC 1 A_WeaponReady (WRF_ALLOWZOOM)
    Goto ReadyCycleCharged
//=================================================================================
  ReadyBeep:
	TNT1 A 0 A_GiveInventory("RLNuclearBiggestFuckingGunCharged",1)
	TNT1 A 0 A_PlaySound("weapons/nukebfgxbeep", 7)
    TNT1 A 0
    Goto ReadyCycleCharged
  ScavengerDrop:
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"WeaponDropConfirm")
	TNT1 A 0 A_GiveInventory("RLScavengerDrop",1)
	TNT1 A 0 A_Print("Press again to drop current weapon, or press Alt Fire to scrap it for a Mod Pack.")
	Goto AfterModDrop
  WeaponDropConfirm:
	TNT1 A 0 A_JumpIfInventory("RLShellshockPerk",1, 1)
	Goto WeaponDropConfirm+2
	TNT1 A 0 A_GiveInventory("RLRenegadeAssemblyCheck",1)
	TNT1 A 0 A_JumpIfInventory("RLNuclearPlasmaPistol",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLHighPowerNuclearPlasmaPistol",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLStormNuclearPlasmaPistol",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLSuperchargedNuclearPlasmaPistol",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLNuclearPlasmaRifle",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLAssaultRifleNuclearPlasmaRifle",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonNuclearPlasmaRifle",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLHighPowerNuclearPlasmaRifle",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLNuclearBFG9000",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLHighPowerNuclearBFG9000",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_JumpIfInventory("RLNuclearVBFG9000",1,"WeaponDropConfirmContinue")
	TNT1 A 0 A_TakeInventory("RLNuclearWeaponSetBonusWeapon", 1)
	TNT1 A 0 A_TakeInventory("RLNuclearWeaponSetBonusActive", 1)
  WeaponDropConfirmContinue:
    TNT1 A 0 A_PlaySound("weapons/nukebfgxoff", 6, 1, 0)
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLNuclearBiggestFuckingGunSelected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
	TNT1 A 0 A_FireCustomMissile("DroppedRLNuclearBiggestFuckingGun",0,0,0)
	TNT1 A 1 A_TakeInventory("RLNuclearBiggestFuckingGun",1)
	Stop
  AfterModDrop:
    PRCG ABCABCABC 1 A_WeaponReady (16)
    Goto Ready
  RenegadeAssemblyNoFire:
	TNT1 A 0 A_TakeInventory("RLRenegadeAssemblyCheck",1)
    PRCG ABCABCABC 1 A_WeaponReady (WRF_NOFIRE | WRF_NOBOB)
    Goto Ready
  Deselect:
	TNT1 A 0
    TNT1 A 0 A_StopSound(CHAN_WEAPON)
    TNT1 A 0 A_PlaySound("weapons/nukebfgxoff", 6, 1, 0)
	TNT1 A 0 A_TakeInventory("RLArmorModItemActivate",1)
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
	TNT1 A 0 A_TakeInventory("RLUseBulkMod",1)
	TNT1 A 0 A_TakeInventory("RLUsePowerMod",1)
	TNT1 A 0 A_TakeInventory("RLUseAgilityMod",1)
	TNT1 A 0 A_TakeInventory("RLUseTechnicalMod",1)
	TNT1 A 0 A_TakeInventory("RLUseNanoMod",1)
	TNT1 A 0 A_TakeInventory("RLUseFirestormMod",1)
	TNT1 A 0 A_TakeInventory("RLUseSniperMod",1)
	TNT1 A 0 A_TakeInventory("RLNuclearBiggestFuckingGunSelected", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
  DeselectLoop:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "DeselectRenegade")
    PRCG A 2 A_Lower
    Loop
  DeselectRenegade:
    TNT1 A 0 A_Lower
    loop
  SelectRenegade: 
    TNT1 A 0 A_Raise
    loop
  SelectSetWeapon:
	TNT1 A 0 A_GiveInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_GiveInventory("RLNuclearBiggestFuckingGunSelected", 1)
    Goto SelectLoop
  SelectLoop:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "SelectRenegade")
    PRCG A 2 A_Raise
    Loop
  Fire:
	TNT1 A 0 A_JumpIfNoAmmo("Ready")
	TNT1 A 0 A_AlertMonsters
    TNT1 A 0 A_PlaySound("weapons/nukebfgxcharge", CHAN_WEAPON)
	PRCG DEDEDEDEDE 3 Bright
    TNT1 A 0 A_PlaySound("weapons/nuclearbfgxalarm", 5, 1, 0, 0.5)
	TNT1 A 0 A_AlertMonsters
	PRCG DEDEDEDEDE 3 Bright
	PRCG DEDEDEDEDEDEDEDEDEDE 2 Bright
	TNT1 A 0 A_AlertMonsters
	PRCG DEDEDEDEDEDEDEDEDEDEDEDEDEDEDEDEDEDEDEDEDEDEDEDEDE 1 Bright
	TNT1 A 0 A_AlertMonsters
	TNT1 A 0 A_GunFlash
	TNT1 A 0 A_FireCustomMissile ("RLBiggestFuckingGunBall", 0, 1)
    Goto FireFinish
  FireFinish:
    PRCG FGH 2 Bright
    PRCG ED 4 Bright
    PRCG CB 6
    PRCG A 6 A_Refire
    Goto Ready
  Flash:
	TNT1 A 2 Bright A_Light(5)
	TNT1 A 2 Bright A_Light2
	TNT1 A 2 Bright A_Light1
	Goto LightDone
  Spawn:
    PRCW A -1
    Stop
  Altfire:
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate",1, "ArmorModConversion")
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"ScavengerTime")
	Goto Ready
  ScavengerTime:
	TNT1 A 0 A_Print("Mod pack extracted from discarded weapon.")
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLNuclearBiggestFuckingGunSelected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
    TNT1 A 0 A_StopSound(6)
	TNT1 A 0 A_JumpIfInventory("RLNuclearPlasmaPistol",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLHighPowerNuclearPlasmaPistol",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLStormNuclearPlasmaPistol",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLSuperchargedNuclearPlasmaPistol",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLNuclearPlasmaRifle",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLAssaultRifleNuclearPlasmaRifle",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLBurstCannonNuclearPlasmaRifle",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLHighPowerNuclearPlasmaRifle",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLNuclearBFG9000",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLHighPowerNuclearBFG9000",1,"ScavengerContinue")
	TNT1 A 0 A_JumpIfInventory("RLNuclearVBFG9000",1,"ScavengerContinue")
	TNT1 A 0 A_TakeInventory("RLNuclearWeaponSetBonusWeapon", 1)
	TNT1 A 0 A_TakeInventory("RLNuclearWeaponSetBonusActive", 1)
  ScavengerContinue:
	TNT1 A 0 A_Jump(256,1,2,3,4)
	TNT1 A 0 A_FireCustomMissile("DroppedRLBulkMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLBulkMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLFirestormMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLFirestormMod",0,0,0)
	Goto ScavengerTimeEnd
  ScavengerTimeEnd:
	TNT1 A 1 A_TakeInventory("RLNuclearBiggestFuckingGun",1)
	Stop
  }
}

//-------------------------------------------------------------------
//		MECHANICS

Actor RLNuclearBiggestFuckingGunSelected : Inventory
{
inventory.maxamount 1
}

Actor RLNuclearBiggestFuckingGunCharged : Inventory
{
inventory.maxamount 1
}

ACTOR RLNuclearBiggestFuckingGunClip : Ammo
{
   +INVENTORY.IGNORESKILL
   Inventory.MaxAmount 130
   Inventory.Icon ""
}

ACTOR RLNuclearBiggestFuckingGunPickup : CustomInventory
{
	Radius 16
	Height 8
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
	Spawn:
		PRCW A -1
		Stop
	Pickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("RLNuclearBiggestFuckingGun",1,"NotPickup") // If the player already has this weapon, they are not given the weapon
		TNT1 A 0 A_JumpIfInventory("RLWeaponLimit",0,"NotPickup") // The absolute max limit check for weapons
		TNT1 A 0 A_PlaySound("weapons/nukebfgxpickup")
		TNT1 A 0 A_GiveInventory("RLNuclearBiggestFuckingGun",1)
		TNT1 A 0 A_GiveInventory("RLWeaponLimit",1)
		TNT1 A 0 A_GiveInventory("RLNuclearWeaponSetBonusWeapon",1)
		TNT1 A 0 A_JumpIfInventory("RLNuclearWeaponSetBonusArmor",1,2)
		TNT1 A 0 A_Print("Picked up the \cvBiggest Fucking Nuclear Gun. \c-Time to take some names.")
		Stop
		TNT1 A 0 A_GiveInventory("RLNuclearWeaponSetBonusActive",1)
		TNT1 A 0 A_Print("Picked up the \cvBiggest Fucking Nuclear Gun.\n\c-= FURIOUS FUSION =\n\cgNuclear weaponry recharges twice as fast", 8, "CONFONT")
		Stop
	NotPickup:
		TNT1 A 0 A_JumpIfInventory("DRLA_WeaponAntiLogFloodCheck",1,"NotPickup_NoPrint")
		TNT1 A 0 A_GiveInventory("DRLA_WeaponAntiLogFloodCheck")
		TNT1 A 0 A_Print("\cv=M= Biggest Fucking Nuclear Gun =M=", 2, "CONFONT")
	 NotPickup_NoPrint:	
		TNT1 A 0
		Fail
	}
}

ACTOR DroppedRLNuclearBiggestFuckingGun
{
	PROJECTILE
	-NOGRAVITY
	Damage 0
	Radius 16
	Height 8
	Speed 9
	DeathSound "weapons/gundrop"
	States
	{
	Spawn:
		PRCW A 1
		Loop
	Death:
		TNT1 A 0
		PRCW A 1 A_SpawnItem("RLNuclearBiggestFuckingGunPickup",0,0,0,0)
		Stop
	}
}
