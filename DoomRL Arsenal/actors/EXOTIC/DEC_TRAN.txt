
ACTOR RLCombatTranslocator : RLWeapon
{
  Inventory.Pickupsound "misc/w_pkup"
  Weapon.UpSound "weapons/weaponholster"
  Obituary "%o was somehow warped to death by %k's translocator."
  Inventory.Pickupmessage "You picked up a Combat Translocator."
  Decal BulletChip
  +AMMO_OPTIONAL
  +NOALERT
  +INVENTORY.UNTOSSABLE
  Tag "Combat Translocator"
  Weapon.Kickback 0
  Weapon.SelectionOrder 1900
  Weapon.SlotNumber 6
  Weapon.AmmoUse1 10
  Weapon.AmmoGive1 0
  Weapon.AmmoType1 "RLCombatTranslocatorClip"
  Weapon.AmmoType2 "Cell"
  States
  {
  Ready:
	TNT1 A 0 A_JumpIfInventory("RLRenegadeAssemblyCheck",1,"RenegadeAssemblyNoFire")
    TNT1 A 0 A_JumpIfTargetInLOS ("ReadyTarget")
    CTLO B 1 A_WeaponReady (48)
    Loop
  ReadyTarget:
    CTLO A 8 A_WeaponReady (48)
    Goto Ready
  ScavengerDrop:
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"WeaponDropConfirm")
	TNT1 A 0 A_GiveInventory("RLScavengerDrop",1)
	TNT1 A 0 A_Print("Press again to drop current weapon, or press Alt Fire to scrap it for a Mod Pack.")
	Goto AfterModDrop
  WeaponDropConfirm:
	TNT1 A 0 A_JumpIfInventory("RLShellshockPerk",1, 1)
	Goto WeaponDropConfirm+2
	TNT1 A 0 A_GiveInventory("RLRenegadeAssemblyCheck",1)
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLCombatTranslocatorSelected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
	TNT1 A 0 A_FireCustomMissile("DroppedRLCombatTranslocator",0,0,0)
	TNT1 A 1 A_TakeInventory("RLCombatTranslocator",1)
	Stop
  AfterModDrop:
    CTLO B 8 A_WeaponReady (16)
    Goto Ready
  RenegadeAssemblyNoFire:
	TNT1 A 0 A_TakeInventory("RLRenegadeAssemblyCheck",1)
    CTLO B 9 A_WeaponReady (WRF_NOFIRE | WRF_NOBOB)
    Goto Ready
  Deselect:
	TNT1 A 0
	TNT1 A 0 A_TakeInventory("RLArmorModItemActivate",1)
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLWeaponDrop",1)
	TNT1 A 0 A_TakeInventory("RLUseBulkMod",1)
	TNT1 A 0 A_TakeInventory("RLUsePowerMod",1)
	TNT1 A 0 A_TakeInventory("RLUseAgilityMod",1)
	TNT1 A 0 A_TakeInventory("RLUseTechnicalMod",1)
	TNT1 A 0 A_TakeInventory("RLUseNanoMod",1)
	TNT1 A 0 A_TakeInventory("RLUseFirestormMod",1)
	TNT1 A 0 A_TakeInventory("RLUseSniperMod",1)
	TNT1 A 0 A_TakeInventory("RLCombatTranslocatorSelected", 1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
  DeselectLoop:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "DeselectRenegade")
    CTLO B 1 A_Lower
    Loop
  DeselectRenegade:
    TNT1 A 0 A_Lower
    loop
  SelectRenegade: 
    TNT1 A 0 A_Raise
    loop
  SelectSetWeapon:
	TNT1 A 0 A_GiveInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_GiveInventory("RLCombatTranslocatorSelected", 1)
    Goto SelectLoop
  SelectLoop:
    TNT1 A 0 A_JumpIfInventory("RLShellshockPerk", 1, "SelectRenegade")
    CTLO B 1 A_Raise
    Loop
  Fire:
    CTLO B 2
	TNT1 A 0 A_JumpIfNoAmmo("Reload")
    TNT1 A 0 A_PlaySound("weapons/combattranslocatorbeep", CHAN_WEAPON)
	TNT1 A 0 A_AlertMonsters
    CTLO A 8
	TNT1 A 0 A_FireCustomMissile("RLCombatTranslocatorShot",0,1)
    Goto FireFinish
  FireFinish:
    CTLO A 2 A_GunFlash
    CTLO B 28 A_WeaponReady (62)
    CTLO B 3 A_ReFire
    Goto Ready
  Reload:
	CTLO B 1 A_WeaponReady (WRF_NOPRIMARY | WRF_ALLOWZOOM)
	TNT1 A 0 A_JumpIfInventory("RLCombatTranslocatorClip", 60, "Ready")
	TNT1 A 0 A_JumpIfInventory("Cell", 1, 1)
	Goto Ready
	TNT1 A 0 // Play the reload animation.
    CTLO C 2
    CTLO D 2
    CTLO E 2
    TNT1 A 0 A_PlaySound("weapons/combattranslocatorunload", 7)
    CTLO E 16
  ReloadWorking:
	TNT1 A 0 A_TakeInventory("Cell", 1)
	TNT1 A 0 A_GiveInventory("RLCombatTranslocatorClip", 1)
	TNT1 A 0 A_JumpIfInventory("RLCombatTranslocatorClip",60,2)
	TNT1 A 0 A_JumpIfInventory("Cell",1,2)
	TNT1 A 0
	Goto ReloadFinish
	TNT1 A 0
	Goto ReloadWorking
  ReloadFinish:
    TNT1 A 0 A_PlaySound("weapons/combattranslocatorload", 5)
    CTLO E 6
    TNT1 A 0 A_PlaySound("weapons/combattranslocatorpump", 7)
    CTLO F 2
    TNT1 A 0 A_PlaySound("weapons/combattranslocatorspark", CHAN_WEAPON)
    CTLO G 2 A_GunFlash ("ReloadFlash")
    CTLO H 2
    CTLO I 2
    CTLO J 2
    CTLO K 2
    CTLO L 2
    CTLO M 2
    CTLO N 2
	Goto Ready
  Flash:
    TNT1 A 2 A_Light (10)
    TNT1 A 2 A_Light1
    Goto LightDone
  ReloadFlash:
    CTLO G 2 Bright A_Light (4)
    CTLO H 2 Bright A_Light (2)
    CTLO I 2 Bright A_Light1
    CTLO J 2 Bright A_Light0
    CTLO K 2 Bright
    CTLO L 2 Bright
    CTLO M 2 Bright
    CTLO N 2 Bright
    Goto LightDone
  Spawn:
    CTLO X -1
    Stop
  Altfire:
	TNT1 A 0 A_JumpIfInventory("RLArmorModItemActivate",1, "ArmorModConversion")
	TNT1 A 0 A_JumpIfInventory("RLScavengerDrop",1,"ScavengerTime")
    CTLO B 2
	TNT1 A 0 A_JumpIfInventory("RLCombatTranslocatorClip", 10, 1)
    Goto Reload
    TNT1 A 0 A_PlaySound("weapons/combattranslocatorbeep", CHAN_WEAPON)
	TNT1 A 0 A_AlertMonsters
    CTLO A 8
	TNT1 A 0 A_TakeInventory("RLCombatTranslocatorClip", 10, TIF_NOTAKEINFINITE)
	TNT1 A 0 A_FireCustomMissile("RLCombatTranslocatorTeleportLocator",0,0)
    CTLO A 2 A_GunFlash
    CTLO B 28 A_WeaponReady (62)
    CTLO B 3 A_ReFire
    Goto Ready
  ScavengerTime:
	TNT1 A 0 A_Print("Mod pack extracted from discarded weapon.")
	TNT1 A 0 A_TakeInventory("RLScavengerDrop",1)
	TNT1 A 0 A_TakeInventory("RLStandardWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLAssembledWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLExoticWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLUniqueWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLLegendaryWeaponToken", 1)
	TNT1 A 0 A_TakeInventory("RLCombatTranslocatorSelected", 1)
	TNT1 A 0 A_TakeInventory("RLWeaponLimit",1)
	TNT1 A 0 A_Jump(32,2,3)
	TNT1 A 0 A_Jump(256,3,4,5,6)
	TNT1 A 0 A_FireCustomMissile("DroppedRLFirestormMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLSniperMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLBulkMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLPowerMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLAgilityMod",0,0,0)
	Goto ScavengerTimeEnd
	TNT1 A 0 A_FireCustomMissile("DroppedRLTechnicalMod",0,0,0)
	Goto ScavengerTimeEnd
  ScavengerTimeEnd:
	TNT1 A 1 A_TakeInventory("RLCombatTranslocator",1)
	Stop
  }
}

//-------------------------------------------------------------------
//		PROJECTILES

ACTOR RLCombatTranslocatorShot : TelOtherFX1
{
  Radius 6
  Height 1
  Speed 100
  States
  {
  Spawn:
    TNT1 A 1
    Loop
  Death:
    TNT1 A 1
    Stop
  }
}

ACTOR RLCombatTranslocatorTeleportLocator : FastProjectile
{
  Radius 16
  Height 56
  Speed 100
  Damage 0
  SeeSound ""
  DeathSound ""
  States
  {
  Spawn:
    TNT1 A 1
    Loop
  Death:
    TNT1 A 1 A_SpawnItemEx("RLCombatTranslocatorTeleportSpot", 0, 0, 0)
    TNT1 A 1 A_GiveToTarget("RLCombatTranslocatorTeleportActivate", 1)
    Stop
  }
}

ACTOR RLCombatTranslocatorTeleportSpot : SpecialSpot
{
  +NOSECTOR
  +NOBLOCKMAP
  States
  {
  Spawn:
    TNT1 A 2
    Stop
  }
}

ACTOR RLCombatTranslocatorTeleportActivate : CustomInventory
{
  -COUNTITEM
  +INVENTORY.ALWAYSPICKUP
  Inventory.PickupMessage ""
  Inventory.PickupSound ""
  States
  {
  Spawn:
    TNT1 A 1
    Stop
  Pickup:
    TNT1 A 0 
	TNT1 A 0 A_Teleport("Teleport", "RLCombatTranslocatorTeleportSpot", "TeleportFog", TF_TELEFRAG)
  Teleport:
	TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Stop
  }
}

//-------------------------------------------------------------------
//		MECHANICS

Actor RLCombatTranslocatorSelected : Inventory
{
inventory.maxamount 1
}

ACTOR RLCombatTranslocatorClip : Ammo
{
   +INVENTORY.IGNORESKILL
   Inventory.MaxAmount 60
   Inventory.Icon ""
}

ACTOR RLCombatTranslocatorPickup : CustomInventory
{
	Radius 16
	Height 8
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	States
	{
	Spawn:
		CTLO X -1
		Stop
	Pickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("RLCombatTranslocator",1,"NotPickup") // If the player already has this weapon, they are not given the weapon
		TNT1 A 0 A_JumpIfInventory("RLWeaponLimit",0,"NotPickup2") // The absolute max limit check for weapons
		TNT1 A 0 A_PlaySound("weapons/combattranslocatorpickup")
		TNT1 A 0 A_GiveInventory("RLCombatTranslocator",1)
		TNT1 A 0 A_GiveInventory("RLWeaponLimit",1)
		TNT1 A 0 A_Print("Picked up a \ctCombat Translocator.")
		Stop
	NotPickup:
		TNT1 A 0 A_JumpIfInventory("Cell",0,1)
		Goto AmmoPickup
	NotPickup2:
		TNT1 A 0 A_JumpIfInventory("DRLA_WeaponAntiLogFloodCheck",1,"NotPickup_NoPrint")
		TNT1 A 0 A_GiveInventory("DRLA_WeaponAntiLogFloodCheck")
		TNT1 A 0 A_Print("\ct= Combat Translocator =", 2, "CONFONT")
	 NotPickup_NoPrint:	
		TNT1 A 0
		Fail
	AmmoPickup:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("Cell",0,"AmmoFull")
		TNT1 A 0 A_PlaySound("misc/w_pkup")
		TNT1 A 1 A_GiveInventory("Cell",60)
		Stop
	AmmoFull:
		CTLO X 1
		Fail
	}
}

ACTOR DroppedRLCombatTranslocator
{
	PROJECTILE
	-NOGRAVITY
	Damage 0
	Radius 16
	Height 8
	Speed 9
	DeathSound "weapons/gundrop"
	States
	{
	Spawn:
		CTLO X 1
		Loop
	Death:
		TNT1 A 0
		CTLO X 1 A_SpawnItem("RLCombatTranslocatorPickup",0,0,0,0)
		Stop
	}
}
